#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ТаблицаОбъектовКонфигурации Экспорт; // содержит таблицу объектов конфигурации.
Перем СтруктураПроверки Экспорт;		   // содержит журнал проверки.
Перем СписокРасширений Экспорт;			   // содержит список проверяемых расширений.
Перем ФайлОсобенностей Экспорт;			   // содержит путь к файлу с особенностями, которые будут загружены после сбора данных.
Перем СоответствиеВключенныхВПроверкуОбъектов Экспорт; // содержит пути проверяемых объектов, если соответствие пустое,
													   // то проверка происходит согласно исключениям конфигурации
													   // и выбранным объектам на форме запуска проверки.

Перем ПоказыватьПредупреждения Экспорт;	   // Флаг, определяющий необходимость показывать предупреждения пользователю.

Перем ИсходнаяВерсия;
Перем УровеньИнформация;
Перем УровеньОшибка;

#КонецОбласти

#Область ЗапускПроверки

// Проверка конфигурации состоит из двух этапов:
// 1. Сбор структуры и сведений о метаданных - СобратьМетаданныеКонфигурации().
// 2. Проверка на поиск ошибок в разрезе объектов и требований - ПроверитьКонфигурациюНаСоответствиеСтандартам().
//
Функция ЗапуститьПроверку(СоставОбъектовДляПроверки = Неопределено, ПараметрыПроверки = Неопределено) Экспорт
	
	Если ПараметрыПроверки = Неопределено Тогда
		ПараметрыПроверки = ЗаполнитьПараметрыПроверки();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Конфигурация) Тогда
		Возврат НСтр("ru='Не выбрана конфигурация.'");
	КонецЕсли;
	
	Если ПараметрыПроверки.ПроверитьСоединениеСБазой Тогда
		Если НЕ ДемоБазаСуществует(КаталогКонфигурацииКопия) Тогда
			СоздатьКопиюКонфигурации();
		КонецЕсли;
		
		Если НЕ ДемоБазаСуществует(КаталогКонфигурацииКопия) Тогда
			Возврат НСтр("ru='Проверяемая база не существует в указанном каталоге.'");
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПроверки.ПроверитьПлатформуДляЗапускаПроверки Тогда
		Статус = ПроверитьПлатформуДляЗапускаПроверки();
		Если НЕ ПустаяСтрока(Статус) Тогда
			Возврат Статус;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПроверки.ПроверитьСоединениеСБазой Тогда
		Статус = ПроверитьСоединениеСБазой();
		Если НЕ ПустаяСтрока(Статус) Тогда
			Возврат Статус;
		КонецЕсли;
		
		// Регистрируем COM-объект Application для текущего пользователя.
		Результат = РаботаСВнешнимСоединением.ЗарегистрироватьCOMApplication(Конфигурация.СтрокаЗапускаПлатформы);
		Если НЕ Результат.Успешно Тогда
			Возврат Результат.Сообщение;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыПроверки.ЗагрузитьКонфигурациюИзХранилища Тогда
		Статус = ЗагрузитьКонфигурациюИзХранилища(ПараметрыПроверки.НастройкиХранилища);
		Если НЕ ПустаяСтрока(Статус) Тогда
			Возврат Статус;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПроверки.ПроверитьВерсиюКонфигурации Тогда
		Статус = ПроверитьВерсиюКонфигурации();
		Если НЕ ПустаяСтрока(Статус) Тогда
			Возврат Статус;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПроверки.ОпределятьРежимЗапуска Тогда
		
		ЗаписатьДокументПроверкиВерсии(, Ложь);
		
		#Если Клиент Тогда
		Результат = РаботаСВнешнимСоединением.КонфигурацияСоответствуетБД(Конфигурация, КаталогКонфигурацииКопия,
			Пользователь, Пароль);
		Если НЕ Результат Тогда
			Если ПоказыватьПредупреждения Тогда
				ТекстПредупреждения = НСтр("ru='Конфигурация базы данных не соответствует сохраненной конфигурации.
					|Рекомендуется обновить конфигурацию базы данных перед продолжением проверки.'");
				Предупреждение(ТекстПредупреждения);
			КонецЕсли;
		КонецЕсли;
		
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
		ЗаписатьДокументПроверкиВерсии(, Ложь);
		РежимЗапуска = ОпределитьРежимЗапускаПриложения();
		Если НЕ ПустаяСтрока(РежимЗапуска) Тогда
			Возврат РежимЗапуска;
		КонецЕсли;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
	Результат = ИнтеграцияССППР.ИнициализироватьСтруктуруДляИнтеграцииВСППР(Конфигурация);
	Если НЕ Результат.Успешно Тогда
		Возврат Результат.ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьСоставПроверки(ПараметрыПроверки, СоставОбъектовДляПроверки);
	
	СтруктураПроверкиДляРегистрацииВСППР = Результат.СтруктураПроверкиДляРегистрацииВСППР;
	ПараметрыПроверки.Вставить("СтруктураПроверкиДляРегистрацииВСППР", СтруктураПроверкиДляРегистрацииВСППР);
	ПараметрыПроверки.Вставить("ФайлРезультатаПлатформеннойПроверки", "");
	
	СборщикДанных = Обработки.СборДанных.Создать();
	СборщикДанных.Версия = Версия;
	СборщикДанных.Конфигурация = Конфигурация;
	СборщикДанных.КаталогКонфигурации = КаталогКонфигурацииКопия;
	СборщикДанных.Пользователь = Пользователь;
	СборщикДанных.Пароль = Пароль;
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.ПоказыватьПредупреждения = ПоказыватьПредупреждения;
	
	СборщикДанных.ИнициализироватьСборДанных();
	
	ПараметрыПроверки.Вставить("ФлагСобраныСведенияОМодулях", Ложь);
	
	ЗаполнитьСоответствиеПроверяемыхОбъектов();
	
	МассивПроверяемыхКорней = СформироватьМассивКонфигурацииИРасширенийДляПроверки();
	
	Результат = ПодготовитьКопииБаз(ПараметрыПроверки, МассивПроверяемыхКорней);
	Если НЕ Результат.Успешно Тогда
		Возврат Результат.ТекстОшибки;
	КонецЕсли;
	
	Для Каждого Корень Из МассивПроверяемыхКорней Цикл
		
		СоставОбъектовДляПроверкиТекущегоКорня = ПолучитьСоставОбъектовДляПроверкиТекущегоКорня(Корень,
			СоставОбъектовДляПроверки);
		
		ПараметрыПроверки.Вставить("СоставОбъектовДляПроверки", СоставОбъектовДляПроверкиТекущегоКорня);
		ПараметрыПроверки.Вставить("Корень", Корень);
		
		Статус = СобратьМетаданныеКонфигурации(СборщикДанных, ПараметрыПроверки);
		
		ПараметрыПроверки.ФлагСобраныСведенияОМодулях = Истина;
		
		ОчиститьМассивВременныхФайлов();
		Если НЕ ПустаяСтрока(Статус) Тогда
			Возврат Статус;
		КонецЕсли;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
		Если ПараметрыПроверки.ПроверитьКонфигурацию Тогда
			Статус = ПроверитьКонфигурациюНаСоответствиеСтандартам(ПараметрыПроверки,
				СоставОбъектовДляПроверкиТекущегоКорня, Корень);
			Если НЕ ПустаяСтрока(Статус) Тогда
				Возврат Статус;
			КонецЕсли;
			
			#Если Клиент Тогда
			ОбработкаПрерыванияПользователя();
			#КонецЕсли
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПараметрыПроверки.ПроверитьСоединениеСБазой Тогда
		ФайлУдалить(КаталогКонфигурацииКопия);
	КонецЕсли;
	ФайлУдалить(ПараметрыПроверки.КаталогКонфигурацииДляПлатформеннойПроверки);
	ФайлУдалить(ПараметрыПроверки.КаталогКонфигурацииДляВыгрузкиВФайлыXML);
	ФайлУдалить(ПараметрыПроверки.КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений);
	
	Если ПараметрыПроверки.ПроверитьКонфигурацию Тогда
		СтруктураПроверки.ОбнаруженоОшибок = ПолучитьОбщееКоличествоОшибокПослеПроверки(Версия);
		
		ОписаниеПроверки = ПолучитьОписаниеПроверки(Сценарий, СписокРасширений);
		
		Текст = СтрШаблон(НСтр("ru='Выполнена проверка %1'"), ОписаниеПроверки);
		Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
			СтруктураПроверки.ТаблицаЖурнала);
		
		ЗаполнитьКоличествоОбъектовДляПроверки(СоставОбъектовДляПроверки);
		
		ДокументПроверки = ПроверкаВерсии.ПолучитьОбъект();
		ДокументПроверки.Проведен = Истина;
		ДокументПроверки.Записать(РежимЗаписиДокумента.Запись);
		
		ЗаписатьДокументПроверкиВерсии();
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция ОпределитьРежимЗапускаПриложения()
	
	Статус = "";
	
	Каталог = КаталогКонфигурацииКопия;
	Если ПустаяСтрока(Каталог) Тогда
		Каталог = Конфигурация.КаталогКонфигурации;
	КонецЕсли;
	
	Если НЕ ДемоБазаСуществует(Каталог) Тогда
		Статус = НСтр("ru='Проверяемая база не существует в указанном каталоге.'");
		Возврат Статус;
	КонецЕсли;
	
	ТолькоОбычныйРежим = Конфигурация.ТолькоОбычныйРежим;
	
	КлючЗапускаПриложения = РаботаСВнешнимСоединением.ПолучитьРежимЗапускаПриложения(Конфигурация,
		Каталог, Пользователь, Пароль);
	
	Если КлючЗапускаПриложения = "НеПолучен" Тогда
		Статус = НСтр("ru='Не удалось автоматически определить режим запуска конфигурации.
			|По умолчанию конфигурация будет проверяться в управляемом режиме.'");
		ТолькоОбычныйРежим = Ложь;
	ИначеЕсли ПустаяСтрока(КлючЗапускаПриложения) Тогда
		Статус = НСтр("ru='Не удалось подключиться к проверяемой базе. Возможно, не верно указаны логин и пароль.
			|Либо указанная версия платформы не соответствует конфигурации проверяемой базы.'");
		Возврат Статус;
	Иначе
		ТолькоОбычныйРежим = (КлючЗапускаПриложения = " /RunModeOrdinaryApplication");
	КонецЕсли;
	
	ОбъектКонфигурация = Конфигурация.ПолучитьОбъект();
	ОбъектКонфигурация.ТолькоОбычныйРежим = ТолькоОбычныйРежим;
	ОбъектКонфигурация.Записать();
	
	Возврат Статус;
	
КонецФункции

// Проверяет наличие платформы, установленной в конфигурации.
//
Функция ПроверитьПлатформуДляЗапускаПроверки() Экспорт
	
	Если НЕ ЗначениеЗаполнено(Конфигурация) Тогда
		#Если Клиент Тогда
		Конфигурация = ФормаРабочегоСтола.Конфигурация;
		#Иначе
		ТекстСообщения = НСтр("ru='Не удалось запустить проверку конфигурации по причине:
			|Конфигурация не определена.'");
		Возврат ТекстСообщения;
		#КонецЕсли
	КонецЕсли;
	
	СтрокаЗапускаПлатформы = Конфигурация.СтрокаЗапускаПлатформы;
	Статус = ПроверитьПлатформуДляПроверкиКонфигурации(СтрокаЗапускаПлатформы);
	Если ПустаяСтрока(Статус) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru='Не удалось запустить проверку конфигурации ""%2"" по причине:%1%3'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, Символы.ПС, Конфигурация.Наименование, Статус);
	
	Возврат ТекстСообщения;
	
КонецФункции

// Проверяет COM-соединение с информационной базой.
//
Функция ПроверитьСоединениеСБазой()
	
	Текст = НСтр("ru='Выполняется проверка подключения к информационной базе'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	Результат = ДоступККонфигуратору.ПроверитьСоединениеСБазой(Конфигурация, КаталогКонфигурацииКопия, Пользователь,
		Пароль, Ложь);
	
	Сообщение = Результат.Сообщение;
	Если НЕ ПустаяСтрока(Сообщение) Тогда
		Если Результат.Успешно Тогда
			Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Сообщение, СтруктураПроверки.ЖурналПроверки,
				СтруктураПроверки.ТаблицаЖурнала);
			ЗаписатьДокументПроверкиВерсии(, Ложь);
		Иначе
			Текст = СтрШаблон(НСтр("ru='Проверка прекращена.
				|Подключение к информационной базе завершилось с ошибкой:
				|%1'"), Сообщение);
			Возврат Текст;
		КонецЕсли;
	КонецЕсли;
	
	Текст = НСтр("ru='Подключение выполнено успешно'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	Возврат "";
	
КонецФункции

// Проверяет версию проверяемой конфигурации.
// Если уже есть проведенный документ проверки версии с версией выше, чем текущая в демобазе, то проверка прекращается.
//
Функция ПроверитьВерсиюКонфигурации()
	
	ЗапросПоДокументам = Новый Запрос;
	ЗапросПоДокументам.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроверкаВерсии.ВерсияКонфигурации.Код КАК Код
	|ИЗ
	|	Документ.ПроверкаВерсии КАК ПроверкаВерсии
	|ГДЕ
	|	ПроверкаВерсии.Конфигурация = &Конфигурация
	|	И НЕ ПроверкаВерсии.ПометкаУдаления
	|	И ПроверкаВерсии.Проведен";
	
	ЗапросПоДокументам.УстановитьПараметр("Конфигурация", Конфигурация);
	
	ТаблицаВерсий = ЗапросПоДокументам.Выполнить().Выгрузить();
	Если ТаблицаВерсий.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	МаксимальнаяВерсия = "0.0.0";
	Для Каждого СтрокаВерсии Из ТаблицаВерсий Цикл
		
		ВерсияКод = СтрокаВерсии.Код;
		Если ПустаяСтрока(ВерсияКод) Тогда
			Продолжить;
		КонецЕсли;
		
		ВерсияКод = СокрЛП(ВерсияКод);
		
		ВерсияКод = ПолучитьНомерВерсииБезСборки(ВерсияКод);
		Если РелизыПоПорядку(МаксимальнаяВерсия, ВерсияКод) Тогда
			МаксимальнаяВерсия = ВерсияКод;
		КонецЕсли;
		
	КонецЦикла;
	
	Если МаксимальнаяВерсия = "0.0.0" Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = РаботаСВнешнимСоединением.ПолучитьВерсиюКонфигурации(Конфигурация, КаталогКонфигурацииКопия,
		Пользователь, Пароль);
	
	Если НЕ Результат.Успешно Тогда
		Возврат СтрШаблон(НСтр("ru='Проверка прекращена.%1%2'"), Символы.ПС, Результат.Сообщение);
	КонецЕсли;
	
	ТекущаяВерсияКонфигурации = Результат.Сообщение;
	Если ПустаяСтрока(ТекущаяВерсияКонфигурации) Тогда
		ТекущаяВерсияКонфигурации = "0.0.0";
	Иначе
		ТекущаяВерсияКонфигурации = ПолучитьНомерВерсииБезСборки(ТекущаяВерсияКонфигурации);
	КонецЕсли;
	
	// Сравниваем полученную версию конфигурацию демобазы с максимальной существующей версией.
	ТекстОшибки = "";
	Если РелизыПоПорядку(ТекущаяВерсияКонфигурации, МаксимальнаяВерсия) Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru='Версия проверяемой конфигурации (%1) ниже, чем у проверенной ранее (%2).
			|Укажите демобазу с нужной версией конфигурации и перезапустите проверку.'"),
			ТекущаяВерсияКонфигурации,
			МаксимальнаяВерсия);
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

Функция ПроверитьКонфигурациюНаСоответствиеСтандартам(ПараметрыПроверки, СоставОбъектовДляПроверки, Корень)
	
	Расширение = Неопределено;
	Если ТипЗнч(Корень) = Тип("СправочникСсылка.Расширения") Тогда
		Расширение = Корень;
	КонецЕсли;
	
	Если СоставОбъектовДляПроверки = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	#Если Клиент Тогда
	ТекстСостояние = НСтр("ru='Выполняется подготовка к проверке %1 на соответствие стандартам'");
	ТекстСостояние = СтрШаблон(ТекстСостояние, ОписаниеКонфигурацииИлиРасширения);
	Состояние(ТекстСостояние);
	#КонецЕсли
	
	Текст = СтрШаблон(НСтр("ru='Стартовала проверка %1'"), ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	
	ЗаписатьДокументПроверкиВерсии();
	
	// Загрузка особенностей конфигурации.
	#Если Клиент Тогда
	Если НЕ ПустаяСтрока(ФайлОсобенностей) Тогда
		
		ТекстОшибки = "";
		
		Попытка
			ИмпортЭкспортКлиент.ЗагрузитьОсобенности(ФайлОсобенностей, Конфигурация, ТекстОшибки, Истина, Расширение);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			
			ТекстОшибки = НСтр("ru='При загрузке особенностей произошла ошибка:%1%2'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Символы.ПС, ОписаниеОшибки);
		КонецПопытки;
		
		Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
			Зафиксировать(Конфигурация.Наименование, УровеньИнформация, ТекстОшибки, СтруктураПроверки.ЖурналПроверки,
				СтруктураПроверки.ТаблицаЖурнала);
			Возврат ТекстОшибки; 
		КонецЕсли;
		
	КонецЕсли;
	#КонецЕсли
	
	СтруктураПроверкиДляРегистрацииВСППР = ПараметрыПроверки.СтруктураПроверкиДляРегистрацииВСППР;
	ФайлРезультатаПлатформеннойПроверки = ПараметрыПроверки.ФайлРезультатаПлатформеннойПроверки;
	
	ДокументПроверки = ПроверкаВерсии.ПолучитьОбъект();
	
	СтруктураПроверки.Вставить("ПроверкаВерсии", ДокументПроверки);
	
	ТекстОшибки = "";
	Попытка
		Проверка.ОбработкаПроведенияДокументаПроверки(СтруктураПроверки, СтруктураПроверкиДляРегистрацииВСППР,
			ФайлРезультатаПлатформеннойПроверки, Расширение);
		ДокументПроверки.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		
		ТекстОшибки = НСтр("ru='При проверке %1 по требованиям произошла ошибка:%2%3'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеКонфигурацииИлиРасширения, Символы.ПС, ОписаниеОшибки);
	КонецПопытки;
	
	// При проведении журнал проверки мог измениться, получаем измененный текст.
	ЗаполнитьЗначенияСвойств(СтруктураПроверки, ДокументПроверки, "ЖурналПроверки");
	
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		Возврат ТекстОшибки;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область СборСведений

Функция ЗапуститьПлатформеннуюПроверку(Конфигурация, СтруктураПроверки, КаталогКонфигурацииДляПлатформеннойПроверки,
	Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	ИмяРасширения = ?(ЗначениеЗаполнено(Расширение), Расширение.Наименование, "");
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	Результат.Вставить("ФайлРезультатаПлатформеннойПроверки", "");
	
	Текст = НСтр("ru='Запуск платформенной проверки %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	ФайлРезультатаПлатформеннойПроверки = ПолучитьИмяВременногоФайла();
	ДоступККонфигуратору.ЗапуститьПлатформеннуюПроверку(Конфигурация, КаталогКонфигурацииДляПлатформеннойПроверки,
		Пользователь, Пароль, ФайлРезультатаПлатформеннойПроверки, ИмяРасширения);
	
	Результат.Вставить("ФайлРезультатаПлатформеннойПроверки", ФайлРезультатаПлатформеннойПроверки);
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьПрименениеРасширенияКОсновнойКонфигурации(Конфигурация, СтруктураПроверки, СборщикДанных, Расширение)
	
	ИмяРасширения = Расширение.Наименование;
	
	Текст = НСтр("ru='Запуск проверки применения расширения %1'");
	Текст = СтрШаблон(Текст, ИмяРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	ИмяРасширения = Расширение.Наименование;
	РезультатПроверки = ДоступККонфигуратору.ВыполнитьПроверкуПримененияРасширения(Конфигурация, КаталогКонфигурацииКопия,
		Конфигурация.Пользователь, Конфигурация.Пароль, ИмяРасширения);
	
	СборщикДанных.Расширение = Расширение;
	Статус = СборщикДанных.ЗаписатьОшибкиПроверкиПримененияРасширения(РезультатПроверки);
	Если НЕ ПустаяСтрока(Статус) Тогда
		Зафиксировать(Конфигурация.Наименование, УровеньОшибка, Статус, СтруктураПроверки.ЖурналПроверки,
			СтруктураПроверки.ТаблицаЖурнала);
	КонецЕсли;
	
	Текст = НСтр("ru='Проверка применения расширения %1 завершена'");
	Текст = СтрШаблон(Текст, ИмяРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	// Устанавливаем флаг, если в статусе нет ошибки.
	Если ПустаяСтрока(Статус) Тогда
		ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеПоПроверкеПримененияРасширения",, Расширение);
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

Функция ЗапуститьВыгрузкуКонфигурацииВФайлыXML(Конфигурация, СтруктураПроверки, КаталогКонфигурацииДляВыгрузкиВФайлыXML,
	Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	ИмяРасширения = ?(ЗначениеЗаполнено(Расширение), Расширение.Наименование, "");
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	Результат.Вставить("КаталогВыгрузкиФайловXML", "");
	Результат.Вставить("ФайлЛогаВыгрузкиФайловXML", "");
	
	Текст = НСтр("ru='Запуск выгрузки %1 в файлы XML'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	
	КаталогВыгрузкиФайловXML = ПолучитьКаталогВременныхФайлов();
	
	ФайлЛогаВыгрузкиФайловXML = ПолучитьИмяВременногоФайла("txt");
	
	ДоступККонфигуратору.ЗапуститьВыгрузкуКонфигурацииВФайлыXML(Конфигурация, КаталогКонфигурацииДляВыгрузкиВФайлыXML,
		Пользователь, Пароль, КаталогВыгрузкиФайловXML, ФайлЛогаВыгрузкиФайловXML, ИмяРасширения);
	
	СтруктураПроверки.ВременныеФайлы.Добавить(КаталогВыгрузкиФайловXML);
	СтруктураПроверки.ВременныеФайлы.Добавить(ФайлЛогаВыгрузкиФайловXML);
	
	Результат.Вставить("КаталогВыгрузкиФайловXML", КаталогВыгрузкиФайловXML);
	Результат.Вставить("ФайлЛогаВыгрузкиФайловXML", ФайлЛогаВыгрузкиФайловXML);
	Возврат Результат;
	
КонецФункции

Функция ОжидатьВыгрузкуКонфигурацииВФайлыXML(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML,
	ФайлЛогаВыгрузкиФайловXML, Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	Операция = НСтр("ru='Выгрузка %1 в файлы XML'");
	Операция = СтрШаблон(Операция, ОписаниеКонфигурацииИлиРасширения);
	Статус = ОжиданиеЗавершенияОперации(ФайлЛогаВыгрузкиФайловXML, Операция);
	Если НЕ ПустаяСтрока(Статус) Тогда
		Результат.Вставить("ТекстОшибки", Статус);
		Возврат Результат;
	КонецЕсли;
	
	СтатусВыгрузки = ФайлПолучитьТекст(ФайлЛогаВыгрузкиФайловXML);
	Если НЕ ПустаяСтрока(СтатусВыгрузки) Тогда
		
		МассивИсключений = ПолучитьМассивИсключенийОшибокВыгрузки();
		ЭтоИсключение = Ложь;
		Для Каждого Элемент Из МассивИсключений Цикл
			Если СтрНайти(ВРег(СтатусВыгрузки), ВРег(Элемент)) > 0 Тогда
				ЭтоИсключение = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЭтоИсключение Тогда
			ТекстОшибки = НСтр("ru='Выгрузка %1 в файлы XML завершилась с ошибкой:%2%3'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеКонфигурацииИлиРасширения, Символы.ПС, СтатусВыгрузки);
			Результат.Вставить("ТекстОшибки", ТекстОшибки);
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Текст = НСтр("ru='Выгрузка %1 в файлы XML завершена'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	Если НЕ ФайлСуществует(КаталогВыгрузкиФайловXML) Тогда
		ТекстОшибки = НСтр("ru='Не найден каталог выгрузки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеКонфигурацииИлиРасширения);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		Возврат Результат;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОчиститьРанееСобранныеСведения(Конфигурация, СтруктураПроверки, Расширение = Неопределено)
	
	Расширение = ПолучитьСсылкуРасширения(Расширение);
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	СобраныДанныеРасширения = ВерсияПолучитьФлагСбораДанных(Версия, "СобраныДанные", Расширение);
	Если НЕ СобраныДанныеРасширения Тогда
		Возврат Результат;
	КонецЕсли;
	
	Текст = НСтр("ru='Начало очистки сведений о метаданных %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	ОчисткаДанныхСобранныхРанееПоСобираемымОбъектам(Расширение);
	
	Текст = НСтр("ru='Сведения о метаданных %1 очищены'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ЗаписатьСтруктуруКонфигурации(Конфигурация, СтруктураПроверки, СоставОбъектовДляПроверки, КаталогВыгрузкиФайловXML,
	СборщикДанных, Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	Текст = НСтр("ru='Начало сбора сведений о структуре метаданных %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	СборщикДанных.Расширение = ПолучитьСсылкуРасширения(Расширение);
	Статус = ЗаполнитьСтруктуруКонфигурации(СборщикДанных, СоставОбъектовДляПроверки, КаталогВыгрузкиФайловXML);
	Если НЕ ПустаяСтрока(Статус) Тогда
		Результат.Вставить("ТекстОшибки", Статус);
		Возврат Результат;
	КонецЕсли;
	
	Текст = НСтр("ru='Собраны и записаны сведения о структуре метаданных %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	Возврат Результат;
	
КонецФункции

Функция ЗаписатьВерсиюКонфигурации(Конфигурация, СтруктураПроверки, СоставОбъектовДляПроверки, КаталогВыгрузкиФайловXML,
	СборщикДанных, Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	Текст = НСтр("ru='Начало сбора сведений о версии %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	ДанныеКонфигурации = РазборФайловXML.ПолучитьДанныеСтруктурыКонфигурации(КаталогВыгрузкиФайловXML, Истина);
	Если ДанныеКонфигурации = Неопределено Тогда
		Результат.Вставить("ТекстОшибки", НСтр("ru='Не удалось собрать данные о версии конфигурации
			|Проверка прекращена'"));
		Возврат Результат;
	КонецЕсли;
	
	СборщикДанных.УстановитьВерсиюКонфигурации(ДанныеКонфигурации.Версия);
	
	// Установим полученную версию.
	ИсходнаяВерсия = Версия;
	Версия = СборщикДанных.Версия;
	
	Если ЗначениеЗаполнено(ПроверкаВерсии) Тогда
		ДокументПроверки = ПроверкаВерсии.ПолучитьОбъект();
		ДокументПроверки.ВерсияКонфигурации = Версия;
		ДокументПроверки.Эталон = ИсходнаяВерсия;
		ДокументПроверки.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	Текст = НСтр("ru='Собраны и записаны сведения о версии %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	Возврат Результат;
	
КонецФункции

Функция СобратьСведенияОРолях(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML, СборщикДанных,
	Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	Текст = НСтр("ru='Начало сбора сведений о ролях %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	// Получаем данные о ролях.
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.Расширение = ПолучитьСсылкуРасширения(Расширение);
	СборщикДанных.ЗаполнитьСведенияОРолях(КаталогВыгрузкиФайловXML);
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Текст = НСтр("ru='Собраны и записаны сведения о ролях %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОРолях",, СборщикДанных.Расширение);
	
	Возврат Результат;
	
КонецФункции

Функция СобратьСведенияОМетаданных(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML, СборщикДанных, Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	// Получаем сведения о метаданных.
	Текст = НСтр("ru='Начало сбора сведений о метаданных %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.Расширение = ПолучитьСсылкуРасширения(Расширение);
	Статус = СборщикДанных.ЗаполнитьСведенияОМетаданных(КаталогВыгрузкиФайловXML);
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Если НЕ ПустаяСтрока(Статус) Тогда
		Результат.Вставить("ТекстОшибки", Статус);
		Возврат Результат;
	КонецЕсли;
	
	Текст = НСтр("ru='Собраны сведения о метаданных %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	Текст = НСтр("ru='Начало записи сведений о метаданных %1 в информационную базу'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.ЗаписатьДанныеВИБ();
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Текст = НСтр("ru='Сведения о метаданных %1 записаны в информационную базу'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОМетаданных",, СборщикДанных.Расширение);
	
	Возврат Результат;
	
КонецФункции

Функция СобратьСведенияОМодулях(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML, СборщикДанных,
	Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	// Получаем данные о модулях конфигурации
	Текст = НСтр("ru='Начало сбора сведений о модулях %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.Расширение = ПолучитьСсылкуРасширения(Расширение);
	Статус = СборщикДанных.ЗаполнитьСведенияОМодулях(КаталогВыгрузкиФайловXML);
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Если НЕ ПустаяСтрока(Статус) Тогда
		Результат.Вставить("ТекстОшибки", Статус);
		Возврат Результат;
	КонецЕсли;
	
	Текст = НСтр("ru='Собраны сведения о модулях %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	Текст = НСтр("ru='Начало записи сведений о модулях %1 в информационную базу'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.ЗаписатьДанныеВИБ();
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Текст = НСтр("ru='Сведения о модулях %1 записаны в информационную базу'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОМодулях",, СборщикДанных.Расширение);
	
	Возврат Результат;
	
КонецФункции

Функция СобратьСведенияОСправке(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML, СборщикДанных,
	Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	// Получаем данные о справке
	Текст = НСтр("ru='Начало сбора сведений о справке %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.Расширение = ПолучитьСсылкуРасширения(Расширение);
	Статус = СборщикДанных.ЗаполнитьСведенияОСправке(КаталогВыгрузкиФайловXML);
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Если НЕ ПустаяСтрока(Статус) Тогда
		Результат.Вставить("ТекстОшибки", Статус);
		Возврат Результат;
	КонецЕсли;
	
	Текст = НСтр("ru='Собраны сведения о справке %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	Текст = НСтр("ru='Начало записи сведений о справке %1 в информационную базу'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.ЗаписатьДанныеВИБ();
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Текст = НСтр("ru='Сведения о справке %1 записаны в информационную базу'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОСправке",, СборщикДанных.Расширение);
	
	Возврат Результат;
	
КонецФункции

Функция СобратьСведенияОФормахИзXML(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML, СборщикДанных,
	Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	// Получаем данные по формам из файлов XML.
	Текст = НСтр("ru='Начало сбора сведений о формах %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.Расширение = ПолучитьСсылкуРасширения(Расширение);
	Статус = СборщикДанных.ЗаполнитьСведенияОФормахИзXML(КаталогВыгрузкиФайловXML);
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Если НЕ ПустаяСтрока(Статус) Тогда
		Результат.Вставить("ТекстОшибки", Статус);
		Возврат Результат;
	КонецЕсли;
	
	Текст = НСтр("ru='Собраны и записаны сведения о формах %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОФормах",, СборщикДанных.Расширение);
	
	Возврат Результат;
	
КонецФункции

Функция СобратьСведенияОМакетах(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML, СборщикДанных,
	Расширение = Неопределено)
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	// Получаем данные по макетам.
	Текст = НСтр("ru='Начало сбора сведений о макетах %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.Расширение = ПолучитьСсылкуРасширения(Расширение);
	Статус = СборщикДанных.ЗаполнитьСведенияОМакетах(КаталогВыгрузкиФайловXML);
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Если НЕ ПустаяСтрока(Статус) Тогда
		Результат.Вставить("ТекстОшибки", Статус);
		Возврат Результат;
	КонецЕсли;
	
	Текст = НСтр("ru='Собраны сведения о макетах %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	Текст = НСтр("ru='Начало записи сведений о макетах %1 в информационную базу'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.ЗаписатьДанныеВИБ();
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Текст = НСтр("ru='Сведения о макетах %1 записаны в информационную базу'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОМакетах",, СборщикДанных.Расширение);
	
	Возврат Результат;
	
КонецФункции

Функция СобратьСведенияОМодуляхОбычныхФорм(Конфигурация, СтруктураПроверки, СборщикДанных,
	Расширение = Неопределено)
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	// Получаем данные о модулях обычных форм.
	Текст = НСтр("ru='Начало сбора сведений о модулях обычных форм конфигурации'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.Расширение = ПолучитьСсылкуРасширения(Расширение);
	Статус = СборщикДанных.ЗаполнитьСведенияОМодуляхОбычныхФорм();
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Если НЕ ПустаяСтрока(Статус) Тогда
		Результат.Вставить("ТекстОшибки", Статус);
		Возврат Результат;
	КонецЕсли;
	
	Текст = НСтр("ru='Собраны сведения о модулях обычных форм конфигурации'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	Текст = НСтр("ru='Начало записи сведений о модулях обычных форм в информационную базу'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	СборщикДанных.СтруктураПроверки = СтруктураПроверки;
	СборщикДанных.ЗаписатьДанныеВИБ();
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	
	Текст = НСтр("ru='Сведения о модулях обычных форм записаны в информационную базу'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОМодулях",, СборщикДанных.Расширение);
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьФлагСобранныеДанные(Конфигурация, СтруктураПроверки, Знач Расширение = Неопределено)
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстОшибки", "");
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Текст = НСтр("ru='Сбор сведений %1 завершен'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии();
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанные",, Расширение);
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьМассивКонфигурацииИРасширенийДляПроверки()
	
	АнализКонфигурации = (Сценарий = 0) ИЛИ (Сценарий = 2);
	АнализРасширений = (Сценарий > 0);
	
	МассивДляПроверки = Новый Массив;
	
	Если АнализРасширений Тогда
		СписокРасширенийКопия = СписокРасширений.Скопировать();
		
		Если АнализКонфигурации Тогда
			
			ПустаяСсылкаРасширение = Справочники.Расширения.ПустаяСсылка();
			ЭлементСпискаРасширения = СписокРасширенийКопия.НайтиПоЗначению(ПустаяСсылкаРасширение);
			Если ЭлементСпискаРасширения <> Неопределено Тогда
				СписокРасширенийКопия.Удалить(ЭлементСпискаРасширения);
			КонецЕсли;
			
		КонецЕсли;
		
		МассивДляПроверки = СписокРасширенийКопия.ВыгрузитьЗначения();
	КонецЕсли;
	
	Если АнализКонфигурации Тогда
		МассивДляПроверки.Вставить(0, Конфигурация);
	КонецЕсли;
	
	Возврат МассивДляПроверки;
	
КонецФункции

Функция ПолучитьСоставОбъектовДляПроверкиТекущегоКорня(Корень, СоставОбъектовДляПроверки)
	
	Если СоставОбъектовДляПроверки = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Расширение = ?(ТипЗнч(Корень) = Тип("СправочникСсылка.Расширения"), Корень, Справочники.Расширения.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Ссылка В(&СписокОбъектов)
	|	И СтруктураКонфигурации.Расширение = &Расширение";
	
	Запрос.УстановитьПараметр("СписокОбъектов", СоставОбъектовДляПроверки);
	Запрос.УстановитьПараметр("Расширение", Расширение);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	СписокОбъектовТекущегоКорня = РезультатЗапроса.ВыгрузитьКолонку("Ссылка");
	
	Возврат СписокОбъектовТекущегоКорня;
	
КонецФункции

Функция ПодготовитьКопииБаз(ПараметрыПроверки, МассивПроверяемыхКорней)
	
	Результат = Новый Структура;
	Результат.Вставить("Успешно", Ложь);
	Результат.Вставить("ТекстОшибки", Истина);
	
	ПараметрыПроверки.Вставить("ВыгружатьКонфигурациюВФайлыXML", Ложь);
	ПараметрыПроверки.Вставить("ТолькоЗаполнениеВерсииИзКонфигурации", Ложь);
	ПараметрыПроверки.Вставить("КаталогКонфигурацииДляПлатформеннойПроверки", "");
	ПараметрыПроверки.Вставить("КаталогКонфигурацииДляВыгрузкиВФайлыXML", "");
	ПараметрыПроверки.Вставить("КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений", "");
	
	Если МассивПроверяемыхКорней.Количество() = 0 Тогда
		Результат.Успешно = Истина;
		Возврат Результат;
	КонецЕсли;
	
	КопироватьБазуДляВыгрузкиВФайлыXMLРасширений = Истина;
	ПервыйЭлементМассива = МассивПроверяемыхКорней[0];
	Если ТипЗнч(ПервыйЭлементМассива) = Тип("СправочникСсылка.Конфигурации") Тогда
		// Если первым элементом массива является Конфигурация,
		// то создавать отдельную копию базы для выгрузки в XML-файлы расширений не требуется.
		КопироватьБазуДляВыгрузкиВФайлыXMLРасширений = Ложь;
	КонецЕсли;
	
	ЕстьРасширения = Ложь;
	Для Каждого ЭлементМассива Из МассивПроверяемыхКорней Цикл
		Если ТипЗнч(ЭлементМассива) = Тип("СправочникСсылка.Расширения") Тогда
			ЕстьРасширения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЭтапыПроверкиПоТребованиям = ПараметрыПроверки.ЭтапыПроверкиПоТребованиям;
	
	Если ЭтапыПроверкиПоТребованиям.ВыполнитьПлатформеннуюПроверкуКонфигурации Тогда
		КаталогКонфигурацииДляПлатформеннойПроверки = ПолучитьКаталогВременныхФайлов();
		ТекстОшибки = КопироватьБазу(КаталогКонфигурацииКопия, КаталогКонфигурацииДляПлатформеннойПроверки);
		Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
			Результат.Вставить("ТекстОшибки", ТекстОшибки);
			Возврат Результат;
		КонецЕсли;
		
		ПараметрыПроверки.КаталогКонфигурацииДляПлатформеннойПроверки = КаталогКонфигурацииДляПлатформеннойПроверки;
		
	КонецЕсли;
	
	ТолькоЗаполнениеВерсииИзКонфигурации = (Сценарий = 1) И НЕ ЗначениеЗаполнено(Версия);
	ПараметрыПроверки.ТолькоЗаполнениеВерсииИзКонфигурации = ТолькоЗаполнениеВерсииИзКонфигурации;
	
	ВыгружатьКонфигурациюВФайлыXML = ПараметрыПроверки.ПостроитьСтруктуру
	 ИЛИ ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОРолях
	 ИЛИ ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОФормах
	 ИЛИ ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОМодулях
	 ИЛИ ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОСправке
	 ИЛИ ТолькоЗаполнениеВерсииИзКонфигурации;
	
	ПараметрыПроверки.ВыгружатьКонфигурациюВФайлыXML = ВыгружатьКонфигурациюВФайлыXML;
	
	Если ВыгружатьКонфигурациюВФайлыXML Тогда
		
		КаталогКонфигурацииДляВыгрузкиВФайлыXML = ПолучитьКаталогВременныхФайлов();
		ТекстОшибки = КопироватьБазу(КаталогКонфигурацииКопия, КаталогКонфигурацииДляВыгрузкиВФайлыXML);
		Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
			Результат.Вставить("ТекстОшибки", ТекстОшибки);
			Возврат Результат;
		КонецЕсли;
		
		ПараметрыПроверки.КаталогКонфигурацииДляВыгрузкиВФайлыXML = КаталогКонфигурацииДляВыгрузкиВФайлыXML;
		
		Если НЕ КопироватьБазуДляВыгрузкиВФайлыXMLРасширений Тогда
			
			ПараметрыПроверки.КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений = КаталогКонфигурацииДляВыгрузкиВФайлыXML;
			
		ИначеЕсли ЕстьРасширения Тогда
			
			КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений = ПолучитьКаталогВременныхФайлов();
			ТекстОшибки = КопироватьБазу(КаталогКонфигурацииКопия, КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений);
			Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
				Результат.Вставить("ТекстОшибки", ТекстОшибки);
				Возврат Результат;
			КонецЕсли;
			
			ПараметрыПроверки.КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений =
				КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.Успешно = Истина;
	Возврат Результат;
	
КонецФункции

Функция СобратьМетаданныеКонфигурации(СборщикДанных, ПараметрыПроверки)

#Область ИнициализацияПеременных
	
	Корень = ПараметрыПроверки.Корень;
	
	Расширение = Неопределено;
	АнализКонфигурации = Истина;
	АнализРасширения = Ложь;
	
	Если ТипЗнч(Корень) = Тип("СправочникСсылка.Расширения") Тогда
		Расширение = Корень;
		АнализКонфигурации = Ложь;
		АнализРасширения = Истина;
	КонецЕсли;
	
	СоставОбъектовДляПроверки = ПараметрыПроверки.СоставОбъектовДляПроверки;
	ВыгружатьКонфигурациюВФайлыXML = ПараметрыПроверки.ВыгружатьКонфигурациюВФайлыXML;
	ТолькоЗаполнениеВерсииИзКонфигурации = ПараметрыПроверки.ТолькоЗаполнениеВерсииИзКонфигурации;
	ЭтапыПроверкиПоТребованиям = ПараметрыПроверки.ЭтапыПроверкиПоТребованиям;
	
	УстановитьФлагСобраныДанные = Ложь;
	
	// Если выбраны требования с проверкой модулей расширения,
	// то необходимо собирать структуру основной конфигурации и сведения о модулях ее объектов,
	// вне зависимости от того, выбраны они для проверки или нет.
	АнализМодулей = (НЕ ПараметрыПроверки.ФлагСобраныСведенияОМодулях И ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОМодулях);
	
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	СтруктураПроверки.Вставить("ОсновнойЯзык", "");
	
#КонецОбласти

#Область ЗапускПлатформеннойПроверки
	
	Если ЭтапыПроверкиПоТребованиям.ВыполнитьПлатформеннуюПроверкуКонфигурации Тогда
		
		КаталогКонфигурацииДляПлатформеннойПроверки = ПараметрыПроверки.КаталогКонфигурацииДляПлатформеннойПроверки;
		
		Результат = ЗапуститьПлатформеннуюПроверку(Конфигурация, СтруктураПроверки,
			КаталогКонфигурацииДляПлатформеннойПроверки, Расширение);
		Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
			Возврат Результат.ТекстОшибки;
		КонецЕсли;
		
		ПараметрыПроверки.Вставить("ФайлРезультатаПлатформеннойПроверки", Результат.ФайлРезультатаПлатформеннойПроверки);
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область ЗапускВыгрузкиКонфигурацииВФайлыXML
	
	КаталогВыгрузкиФайловXML = "";
	
	Если ВыгружатьКонфигурациюВФайлыXML Тогда
		
		Если АнализКонфигурации ИЛИ АнализМодулей ИЛИ ТолькоЗаполнениеВерсииИзКонфигурации Тогда
			
			КаталогКонфигурацииДляВыгрузкиВФайлыXML = ПараметрыПроверки.КаталогКонфигурацииДляВыгрузкиВФайлыXML;
			
			Результат = ЗапуститьВыгрузкуКонфигурацииВФайлыXML(Конфигурация, СтруктураПроверки,
				КаталогКонфигурацииДляВыгрузкиВФайлыXML);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			КаталогВыгрузкиФайловXML = Результат.КаталогВыгрузкиФайловXML;
			ФайлЛогаВыгрузкиФайловXML = Результат.ФайлЛогаВыгрузкиФайловXML;
			
		КонецЕсли;
		
		Если АнализРасширения Тогда
			
			КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений = ПараметрыПроверки.КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений;
			
			Результат = ЗапуститьВыгрузкуКонфигурацииВФайлыXML(Конфигурация, СтруктураПроверки,
				КаталогКонфигурацииДляВыгрузкиВФайлыXMLРасширений, Расширение);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			КаталогВыгрузкиФайловXMLРасширения = Результат.КаталогВыгрузкиФайловXML;
			ФайлЛогаВыгрузкиФайловXMLРасширения = Результат.ФайлЛогаВыгрузкиФайловXML;
			
			#Если Клиент Тогда
			ОбработкаПрерыванияПользователя();
			#КонецЕсли
		
		КонецЕсли;
		
		КаталогВыгрузки = ?(АнализКонфигурации, КаталогВыгрузкиФайловXML, КаталогВыгрузкиФайловXMLРасширения);
		
	КонецЕсли;
	
#КонецОбласти

#Область ОжиданиеВыгрузкиКонфигурацииВФайлыXML
	
	Если ВыгружатьКонфигурациюВФайлыXML Тогда
		
		Если АнализКонфигурации ИЛИ АнализМодулей ИЛИ ТолькоЗаполнениеВерсииИзКонфигурации Тогда
			
			Результат = ОжидатьВыгрузкуКонфигурацииВФайлыXML(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML,
				ФайлЛогаВыгрузкиФайловXML);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
		Если АнализРасширения Тогда
			
			Результат = ОжидатьВыгрузкуКонфигурацииВФайлыXML(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXMLРасширения,
				ФайлЛогаВыгрузкиФайловXMLРасширения, Расширение);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область ЗаписьСтруктурыКонфигурации
	
	Если ПараметрыПроверки.ПостроитьСтруктуру Тогда
		
		Если ТолькоЗаполнениеВерсииИзКонфигурации Тогда
			
			Результат = ЗаписатьВерсиюКонфигурации(Конфигурация, СтруктураПроверки, СоставОбъектовДляПроверки,
				КаталогВыгрузкиФайловXML, СборщикДанных);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
		// Строим структуру конфигурации, даже если выбрана проверка расширений, но по модулям,
		// т.к. для некоторых проверок модулей необходимо знать структуру основной конфигурации.
		Если АнализКонфигурации ИЛИ АнализМодулей Тогда
			
			Результат = ЗаписатьСтруктуруКонфигурации(Конфигурация, СтруктураПроверки, СоставОбъектовДляПроверки,
				КаталогВыгрузкиФайловXML, СборщикДанных);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
		Если АнализРасширения Тогда
			
			Результат = ЗаписатьСтруктуруКонфигурации(Конфигурация, СтруктураПроверки, СоставОбъектовДляПроверки,
				КаталогВыгрузкиФайловXMLРасширения, СборщикДанных, Расширение);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область ОчисткаРанееСобранныхСведений
	
	// Очищаем данные, только если они были собраны при предыдущей проверке.
	Если ПараметрыПроверки.ОчищатьСобранныеДанные Тогда
		
		Если АнализКонфигурации ИЛИ АнализМодулей Тогда
			ОчиститьРанееСобранныеСведения(Конфигурация, СтруктураПроверки);
		КонецЕсли;
		
		Если АнализРасширения Тогда
			ОчиститьРанееСобранныеСведения(Конфигурация, СтруктураПроверки, Расширение);
		КонецЕсли;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область УстановкаФлагаСобраныДанные
	
	// Устанавливаем флаги общего сбора данных, т.к. дальше точно будет что-то собрано.
	Если АнализКонфигурации ИЛИ АнализМодулей Тогда
		ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанные");
	КонецЕсли;
	
	Если АнализРасширения Тогда
		ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанные",, Расширение);
	КонецЕсли;
	
#КонецОбласти

#Область СборСведенийОРолях
	
	Если ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОРолях Тогда
		
		СобратьСведенияОРолях(Конфигурация, СтруктураПроверки, КаталогВыгрузки, СборщикДанных, Расширение);
		
		УстановитьФлагСобраныДанные = Истина;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область СборСведенийОМетаданных
	
	Если ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОМетаданных Тогда
		
		Результат = СобратьСведенияОМетаданных(Конфигурация, СтруктураПроверки, КаталогВыгрузки, СборщикДанных, Расширение);
		Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
			Возврат Результат.ТекстОшибки;
		КонецЕсли;
		
		УстановитьФлагСобраныДанные = Истина;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область СборСведенийОМодулях
	
	Если ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОМодулях Тогда
		
		Если АнализМодулей Тогда
			// Получаем сведения о модулях, даже если выбрана проверка только расширений,
			// т.к. для некоторых проверок модулей объектов расширения необходимо знать содержимое модулей основной конфигурации.
			Результат = СобратьСведенияОМодулях(Конфигурация, СтруктураПроверки, КаталогВыгрузкиФайловXML, СборщикДанных);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
			Результат = СобратьСведенияОМодуляхОбычныхФорм(Конфигурация, СтруктураПроверки, СборщикДанных);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
		Если АнализРасширения Тогда
			
			Результат = СобратьСведенияОМодулях(Конфигурация, СтруктураПроверки, КаталогВыгрузки, СборщикДанных, Расширение);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
			Результат = СобратьСведенияОМодуляхОбычныхФорм(Конфигурация, СтруктураПроверки, СборщикДанных, Расширение);
			Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
				Возврат Результат.ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
		УстановитьФлагСобраныДанные = Истина;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область СборСведенийОСправке
	
	Если ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОСправке Тогда
		
		Результат = СобратьСведенияОСправке(Конфигурация, СтруктураПроверки, КаталогВыгрузки, СборщикДанных, Расширение);
		Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
			Возврат Результат.ТекстОшибки;
		КонецЕсли;
		
		УстановитьФлагСобраныДанные = Истина;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область ПроверкаПримененияРасширений
	
	Если АнализРасширения И ЭтапыПроверкиПоТребованиям.ВыполнитьПроверкуПримененияРасширения Тогда
		
		ПроверитьПрименениеРасширенияКОсновнойКонфигурации(Конфигурация, СтруктураПроверки, СборщикДанных, Расширение);
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область СборСведенийОФормахИзXML
	
	Если ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОФормах Тогда
		
		Результат = СобратьСведенияОФормахИзXML(Конфигурация, СтруктураПроверки, КаталогВыгрузки, СборщикДанных, Расширение);
		Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
			Возврат Результат.ТекстОшибки;
		КонецЕсли;
		
		УстановитьФлагСобраныДанные = Истина;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область СборСведенийОМакетах
	
	Если ЭтапыПроверкиПоТребованиям.ЗаполнитьСведенияОМакетах Тогда
		
		Результат = СобратьСведенияОМакетах(Конфигурация, СтруктураПроверки, КаталогВыгрузки, СборщикДанных, Расширение);
		Если НЕ ПустаяСтрока(Результат.ТекстОшибки) Тогда
			Возврат Результат.ТекстОшибки;
		КонецЕсли;
		
		УстановитьФлагСобраныДанные = Истина;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
	КонецЕсли;
	
#КонецОбласти

#Область ПовторнаяУстановкаФлагаСобраныДанные
	
	// Повторно устанавливаем флаги общего сбора данных, только если что-то действительно было собрано.
	Если УстановитьФлагСобраныДанные Тогда
		
		Если АнализКонфигурации ИЛИ АнализМодулей Тогда
			УстановитьФлагСобранныеДанные(Конфигурация, СтруктураПроверки);
		КонецЕсли;
		
		Если АнализРасширения Тогда
			УстановитьФлагСобранныеДанные(Конфигурация, СтруктураПроверки, Расширение);
		КонецЕсли;
	КонецЕсли;
	
#КонецОбласти
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
КонецФункции

Функция ЗагрузитьКонфигурациюИзХранилища(НастройкиХранилища)
	
	Текст = НСтр("ru='Начало получения номера версии хранилища конфигурации'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	ВерсияХранилищаССобраннымиДанными = ПолучитьВерсиюХранилищаИзДокументовПроверки();
	
	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("Конфигурация", Конфигурация);
	ПараметрыЗапуска.Вставить("СтрокаЗапускаПлатформы", Конфигурация.СтрокаЗапускаПлатформы);
	ПараметрыЗапуска.Вставить("КаталогКонфигурации", КаталогКонфигурацииКопия);
	ПараметрыЗапуска.Вставить("Пользователь", Пользователь);
	ПараметрыЗапуска.Вставить("Пароль", Пароль);
	ПараметрыЗапуска.Вставить("КаталогХранилища", НастройкиХранилища.КаталогХранилища);
	ПараметрыЗапуска.Вставить("ПользовательХранилища", НастройкиХранилища.ПользовательХранилища);
	ПараметрыЗапуска.Вставить("ПарольХранилища", НастройкиХранилища.ПарольХранилища);
	ПараметрыЗапуска.Вставить("ПерезапуститьНаНеобходимойПлатформе", Истина);
	ПараметрыЗапуска.Вставить("ПерезаписатьСтрокуЗапускаПлатформыКонфигурации", Ложь);
	ПараметрыЗапуска.Вставить("ВерсияХранилищаССобраннымиДанными", ВерсияХранилищаССобраннымиДанными);
	
	Результат = ДоступККонфигуратору.ПолучитьВерсиюХранилищаИСписокИзмененныхОбъектов(ПараметрыЗапуска);
	Если Результат.Успешно Тогда
		Текст = НСтр("ru='Номер версии хранилища конфигурации получен успешно: %1'");
		Текст = СтрШаблон(Текст, Результат.ВерсияХранилища);
		Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
			СтруктураПроверки.ТаблицаЖурнала);
		
		Если Сценарий <> 1 И ЗначениеЗаполнено(ПроверкаВерсии) Тогда
			ВерсияХранилища = Результат.ВерсияХранилища;
			ЭтоЧисло(ВерсияХранилища);
			
			ДокументПроверки = ПроверкаВерсии.ПолучитьОбъект();
			ДокументПроверки.ВерсияХранилища = ВерсияХранилища;
			ДокументПроверки.Записать(РежимЗаписиДокумента.Запись);
			
			ДобавитьОбъектыВСоответствиеВключенныхВПроверкуОбъектов(Результат.СписокИзмененныхОбъектов);
			
			Если СоответствиеВключенныхВПроверкуОбъектов <> Неопределено Тогда
				Текст = НСтр("ru='Всего получено измененных объектов из хранилища: %1 из %2 без учета реквизитов, табличных частей, форм и других дочерних объектов'");
				КоличествоИзмененныхОбъектов = СоответствиеВключенныхВПроверкуОбъектов.Количество();
				Если СоответствиеВключенныхВПроверкуОбъектов["Конфигурация"] = Неопределено Тогда
					КоличествоИзмененныхОбъектов = КоличествоИзмененныхОбъектов + 1;
				КонецЕсли;
				КоличествоОбъектовОбщее = ПолучитьОбщееКоличествоОбъектовКонфигурации(Версия, Конфигурация);
				Текст = СтрШаблон(Текст, КоличествоИзмененныхОбъектов, КоличествоОбъектовОбщее);
				Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
					СтруктураПроверки.ТаблицаЖурнала);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Текст = НСтр("ru='Начало обновления конфигурации из хранилища'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	ПараметрыЗапуска.Вставить("ПерезапуститьНаНеобходимойПлатформе", Истина);
	ПараметрыЗапуска.Вставить("ПерезаписатьСтрокуЗапускаПлатформыКонфигурации", Ложь);
	
	Результат = ДоступККонфигуратору.ЗагрузитьКонфигурациюИзХранилищаИОбновить(ПараметрыЗапуска);
	Если НЕ Результат.Успешно Тогда
		ТекстОшибки = НСтр("ru='Обновление конфигурации из хранилища завершилось с ошибкой:%1%2'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Символы.ПС, Результат.ТекстОшибки);
		Возврат ТекстОшибки;
	КонецЕсли;
	
	Текст = НСтр("ru='Обновление конфигурации из хранилища завершено'");
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	
	Текст = НСтр("ru='Начало выполнения обработчиков обновления'");
	
	ТекущаяВерсия = Версия.ПолныйНомер;
	Если НЕ ПустаяСтрока(ТекущаяВерсия) Тогда
		Текст = Текст + " " + СтрШаблон(НСтр("ru='с версии %1'"), ТекущаяВерсия);
	КонецЕсли;
	
	Результат = РаботаСВнешнимСоединением.ПолучитьВерсиюКонфигурации(Конфигурация, КаталогКонфигурацииКопия,
		Пользователь, Пароль);
	Если Результат.Успешно Тогда
		Текст = Текст + " " + СтрШаблон(НСтр("ru='на версию %1'"), Результат.Сообщение);
	КонецЕсли;
	
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	ЗаписатьДокументПроверкиВерсии(, Ложь);
	
	Статус = ДоступК1СПредприятию.ВыполнитьОбработчикОбновления(Конфигурация, КаталогКонфигурацииКопия,
		Пользователь, Пароль);
	
	Текст = ?(ПустаяСтрока(Статус), НСтр("ru='Выполнение обработчиков обновления успешно завершено'"), Статус);
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	
	Возврат "";
	
КонецФункции

Функция ЗаполнитьСтруктуруКонфигурации(СборщикДанных, СоставОбъектовДляПроверки, КаталогВыгрузкиФайловXML)
	
	Результат = СборщикДанных.ЗаполнитьСтруктуруКонфигурации(КаталогВыгрузкиФайловXML,
		СоответствиеВключенныхВПроверкуОбъектов);
	СтруктураПроверки = СборщикДанных.СтруктураПроверки;
	Если НЕ Результат Тогда
		Текст = НСтр("ru='Не удалось собрать данные о структуре конфигурации
			|Проверка конфигурации прекращена'");
		Возврат Текст;
	КонецЕсли;
	
	// Установим полученную версию.
	ИсходнаяВерсия = Версия;
	Версия = СборщикДанных.Версия;
	
	Если ЗначениеЗаполнено(ПроверкаВерсии) Тогда
		ДокументПроверки = ПроверкаВерсии.ПолучитьОбъект();
		ДокументПроверки.ВерсияКонфигурации = Версия;
		ДокументПроверки.Эталон = ИсходнаяВерсия;
		ДокументПроверки.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	СоставОбъектовДляПроверки = СопоставитьОбъектыДляПроверки(СоставОбъектовДляПроверки);
	СопоставитьОтветственных();
	
	ПолучитьТаблицуСтруктурыКонфигурации(Версия);
	
	Возврат "";
	
КонецФункции

// Получает таблицу объектов структуры конфигурации.
// Делать отбор по расширению не требуется, так как необходимо получить все объекты и конфигурации и расширения.
//
Функция ПолучитьТаблицуСтруктурыКонфигурации(Версия) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Ссылка,
	|	СтруктураКонфигурации.Родитель,
	|	СтруктураКонфигурации.ТипОбъекта
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Владелец
	|	И НЕ СтруктураКонфигурации.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Владелец", Версия);
	
	ТаблицаОбъектовКонфигурации = Запрос.Выполнить().Выгрузить();
	
	ТаблицаОбъектовКонфигурации.Индексы.Добавить("Ссылка");
	ТаблицаОбъектовКонфигурации.Индексы.Добавить("Родитель");
	ТаблицаОбъектовКонфигурации.Индексы.Добавить("Ссылка, Родитель");
	
КонецФункции

// Устанавливает ответственных для версии из предыдущей версии
// в соответствии с номером версии.
//
Процедура СопоставитьОтветственных()
	
	Если (НЕ ЗначениеЗаполнено(ИсходнаяВерсия)) ИЛИ (ИсходнаяВерсия = Версия) Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныеОтветственные = Новый Соответствие;
	
	ЗапросПоИсходнымОбъектам = Новый Запрос;
	ЗапросПоИсходнымОбъектам.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Путь КАК Путь,
	|	СтруктураКонфигурации.Ответственный КАК Ответственный
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Версия
	|	И НЕ СтруктураКонфигурации.Ответственный.Ссылка ЕСТЬ NULL";
	
	ЗапросПоИсходнымОбъектам.УстановитьПараметр("Версия", ИсходнаяВерсия);
	ИсходныеОтветственные = ЗапросПоИсходнымОбъектам.Выполнить().Выгрузить();
	ИсходныеОтветственные.Индексы.Добавить("Путь");
	
	ЗапросПоТекущимОбъектам = Новый Запрос;
	ЗапросПоТекущимОбъектам.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Ссылка КАК Ссылка,
	|	СтруктураКонфигурации.Путь КАК Путь
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Версия";
	
	ЗапросПоТекущимОбъектам.УстановитьПараметр("Версия", Версия);
	ВыборкаОбъектов = ЗапросПоТекущимОбъектам.Выполнить().Выбрать();
	
	Пока ВыборкаОбъектов.Следующий() Цикл
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
		ИсходныйОбъект = ИсходныеОтветственные.Найти(ВыборкаОбъектов.Путь, "Путь");
		
		Если ИсходныйОбъект <> Неопределено Тогда
			
			Объект = ВыборкаОбъектов.Ссылка.ПолучитьОбъект();
			Объект.Ответственный = ИсходныйОбъект.Ответственный;
			Объект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Очищает информацию о структуре конфигурации или расширения по версии
//
Функция ОчисткаДанныхСобранныхРанееПоСобираемымОбъектам(Знач Расширение = Неопределено)
	
	ВерсияУстановитьФлагиСбораДанных(Ложь, Расширение);
	
	ЗапросПоОбъектамКонфигурации = Новый Запрос;
	ЗапросПоОбъектамКонфигурации.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Ссылка КАК Объект,
	|	СтруктураКонфигурации.Путь КАК Путь
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Владелец
	|	И СтруктураКонфигурации.СобраныСведения
	|	И НЕ СтруктураКонфигурации.ПометкаУдаления
	|	И СтруктураКонфигурации.Расширение = &Расширение";
	
	ЗапросПоОбъектамКонфигурации.УстановитьПараметр("Расширение", Расширение);
	ЗапросПоОбъектамКонфигурации.УстановитьПараметр("Владелец", Версия);
	Выборка = ЗапросПоОбъектамКонфигурации.Выполнить().Выбрать();
	
	Счетчик = 1;
	КоличествоОбъектов = Выборка.Количество();
	ПроцентОбработанныхОбъектов = 0;
	ТекстСостоянияШаблон = НСтр("ru='Выполняется очистка сведений о метаданных (%1%%)'");
	#Если Клиент Тогда
	Состояние(СтрШаблон(ТекстСостоянияШаблон, "0"));
	#КонецЕсли
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписейСоставныхСвойствОбъектов = РегистрыСведений.ЗначенияСоставныхСвойствОбъектов.СоздатьНаборЗаписей();
		НаборЗаписейСоставныхСвойствОбъектов.Отбор.Объект.Установить(Выборка.Объект);
		НаборЗаписейСоставныхСвойствОбъектов.Записать();
		
		НаборЗаписейСвойствОбъектов = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьНаборЗаписей();
		НаборЗаписейСвойствОбъектов.Отбор.Объект.Установить(Выборка.Объект);
		НаборЗаписейСвойствОбъектов.Записать();
		
		#Если Клиент Тогда
		
		ОбработкаПрерыванияПользователя();
		
		ТекущийПроцентОбъектов = Цел(100 * Счетчик / КоличествоОбъектов);
		
		Если ТекущийПроцентОбъектов > ПроцентОбработанныхОбъектов Тогда
			ПроцентОбработанныхОбъектов = ТекущийПроцентОбъектов;
			ТекстСостояния = СтрШаблон(ТекстСостоянияШаблон, Строка(ТекущийПроцентОбъектов));
			Состояние(ТекстСостояния);
		КонецЕсли;
		
		Счетчик = Счетчик + 1;
		
		#КонецЕсли
		
	КонецЦикла;
	
КонецФункции

Функция ОжиданиеЗавершенияОперации(ФайлРезультатаОперации, НаименованиеОперации = "")
	
	Статус = "";
	
	#Если Клиент Тогда
	
	НачалоОжидания = ТекущаяДатаСеанса();
	
	ФайлСкриптаПаузы = ПолучитьИмяВременногоФайла("vbs");
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.ДобавитьСтроку("WScript.Sleep WScript.Arguments(0)");
	ТекстовыйДокумент.Записать(ФайлСкриптаПаузы, КодировкаТекста.ANSI);
	
	ОбъектWScript = Новый COMОбъект("WScript.Shell");
	КомандаПаузы = "WScript.exe ""%1"" %2";
	КомандаПаузы = СтрШаблон(КомандаПаузы, ФайлСкриптаПаузы, "10000");
	
	ЧтениеТекста = Новый ЧтениеТекста;
	
	ТекстСостояния = НСтр("ru='Ожидание завершения операции ""%1""'");
	ТекстСостояния = СтрШаблон(ТекстСостояния, НаименованиеОперации);
	Состояние(ТекстСостояния);
	
	Пока Истина Цикл
		
		// Пауза 10 секунд.
		ОбъектWScript.Run(КомандаПаузы, 0, 1);
		
		// Ожидаем не более 5 часов.
		ВремяОжидания = ТекущаяДатаСеанса() - НачалоОжидания;
		Если ВремяОжидания > 5 * 60 * 60 Тогда
			Статус = НСтр("ru='Время ожидания завершения операции ""%1"" превышает 5 часов.'");
			Статус = СтрШаблон(Статус, НаименованиеОперации);
			Прервать;
		КонецЕсли;
		
		Если НЕ ФайлСуществует(ФайлРезультатаОперации) Тогда
			Статус = НСтр("ru='Не найден файл с результатами операции ""%1"" по пути: %2'");
			Статус = СтрШаблон(Статус, НаименованиеОперации, ФайлРезультатаОперации);
			Возврат Статус;
		КонецЕсли;
		
		// Проверяем, можно ли открыть файл на чтение монопольно.
		Попытка
			ЧтениеТекста.Открыть(ФайлРезультатаОперации);
			ЧтениеТекста.Закрыть();
			
			// Если да, то платформенная проверка закончилась, прерываем цикл.
			Прервать;
		Исключение
		КонецПопытки;
		
		ОбработкаПрерыванияПользователя();
		
	КонецЦикла;
	
	Состояние("");
	
	ФайлУдалить(ФайлСкриптаПаузы);
	ОбъектWScript = Неопределено;
	
	#КонецЕсли
	
	Возврат Статус;
	
КонецФункции

#КонецОбласти

#Область УправлениеТребованиями

// Получает структуру этапов проверки согласно требованиям.
//
Функция ПолучитьЭтапыПроверкиПоТребованиям(СписокТребований = Неопределено) Экспорт
	
	ТаблицаПроверокПоТребованиям = Новый ТаблицаЗначений;
	
	Если СписокТребований <> Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТребованияЭтапыПроверки.Этап КАК Этап
		|ИЗ
		|	Справочник.Требования.ЭтапыПроверки КАК ТребованияЭтапыПроверки
		|ГДЕ
		|	ТребованияЭтапыПроверки.Ссылка В (&СписокТребований)";
		
		Запрос.УстановитьПараметр("СписокТребований", СписокТребований);
		
		ТаблицаПроверокПоТребованиям = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	ЭтапыПроверкиПоТребованиям = Новый Структура;
	Для Каждого ТекущийЭтап Из Метаданные.Перечисления.ЭтапыПроверки.ЗначенияПеречисления Цикл
		ИмяЭтапа = ТекущийЭтап.Имя;
		
		СтрокаТаблицы = ТаблицаПроверокПоТребованиям.Найти(Перечисления.ЭтапыПроверки[ИмяЭтапа]);
		Выполнять = (СтрокаТаблицы <> Неопределено);
		
		ЭтапыПроверкиПоТребованиям.Вставить(ИмяЭтапа, Выполнять);
	КонецЦикла;
	
	Возврат ЭтапыПроверкиПоТребованиям;
	
КонецФункции

Функция ПолучитьГруппуТребованийРазработкаИИспользованиеБиблиотек()
	
	НаименованиеГруппы = НСтр("ru='Разработка и использование библиотек'");
	СистемаСтандартов = Справочники.Требования.СистемаСтандартов;
	
	ГруппаТребований = Справочники.Требования.НайтиПоНаименованию(НаименованиеГруппы, Истина, СистемаСтандартов);
	
	Если ГруппаТребований = Неопределено Тогда
		ГруппаТребований = Справочники.Требования.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ГруппаТребований;
	
КонецФункции

// Возвращает список требований, выбранный в карточке конфигурации.
//
Функция ПолучитьСписокТребований(КэшУстановленныхФлажков, НомерПроверки) Экспорт
	
	ЗапросПоТребованиям = Новый Запрос;
	ЗапросПоТребованиям.Текст = "
	|ВЫБРАТЬ
	|	Требования.Ссылка КАК Требование
	|ИЗ
	|	Справочник.Требования КАК Требования
	|ГДЕ
	|	НЕ Требования.ЭтоГруппа
	|	И НЕ Требования.ПометкаУдаления
	|	%1
	|	%2
	|	%3
	|	%4
	|УПОРЯДОЧИТЬ ПО
	|	Требования.Код";
	
	Если ТипЗнч(КэшУстановленныхФлажков) = Тип("КоллекцияСтрокДереваЗначений") Тогда
		МассивВыбранныхТребований = КэшУстановленныхФлажков.ВыгрузитьКолонку("Ссылка");
	Иначе
		МассивВыбранныхТребований = Новый Массив;
	КонецЕсли;
	
	УсловиеПоВыбраннымТребованиям = "";
	Если МассивВыбранныхТребований.Количество() > 0 Тогда
		УсловиеПоВыбраннымТребованиям = "И Требования.Ссылка В(&МассивВыбранныхТребований)";
		ЗапросПоТребованиям.УстановитьПараметр("МассивВыбранныхТребований", МассивВыбранныхТребований);
	КонецЕсли;
	
	Если (НомерПроверки = 1) И (НЕ ЗначениеЗаполнено(Версия)) Тогда
		ОпределитьРежимЗапускаПриложения();
	КонецЕсли;
	УсловиеПоРежиму = "И Требования." + ?(Конфигурация.ТолькоОбычныйРежим, "ОбычноеПриложение", "УправляемоеПриложение");
	
	УсловиеПоБиблиотеке = "";
	Если НЕ Конфигурация.ЭтоБиблиотека Тогда
		ГруппаТребований = ПолучитьГруппуТребованийРазработкаИИспользованиеБиблиотек();
		УсловиеПоБиблиотеке = "И НЕ Требования.Родитель = &ГруппаТребований";
		ЗапросПоТребованиям.УстановитьПараметр("ГруппаТребований", ГруппаТребований);
	КонецЕсли;
	
	УсловиеПоДополнительнымТребованиям = "";
	Если НЕ Конфигурация.РасширеннаяПроверкаПоДополнительнымТребованиям Тогда
		МассивВнутреннихТребований = ПолучитьВнутренниеТребования();
		УсловиеПоДополнительнымТребованиям = "И НЕ Требования.Ссылка В(&МассивВнутреннихТребований)";
		ЗапросПоТребованиям.УстановитьПараметр("МассивВнутреннихТребований", МассивВнутреннихТребований);
	КонецЕсли;
	
	ЗапросПоТребованиям.Текст = СтрШаблон(ЗапросПоТребованиям.Текст,
		УсловиеПоВыбраннымТребованиям,
		УсловиеПоРежиму,
		УсловиеПоБиблиотеке,
		УсловиеПоДополнительнымТребованиям);
	
	ТаблицаТребований = ЗапросПоТребованиям.Выполнить().Выгрузить();
	МассивТребований = ТаблицаТребований.ВыгрузитьКолонку("Требование");
	
	СписокТребований = Новый СписокЗначений;
	СписокТребований.ЗагрузитьЗначения(МассивТребований);
	
	Возврат СписокТребований;
	
КонецФункции

// Процедура, необходимая для создания таблицы значений, хранящей
// выбранные пользователем требования в карточке запуска проверки.
//
Процедура ПрочитатьТребования(КэшУстановленныхФлажков, ФлагПроверкаПоРасписанию = Ложь,
	ТекущийВариантПроверки = Неопределено) Экспорт
	
	Если ФлагПроверкаПоРасписанию Тогда
		ВариантПроверки = ВариантПроверкиПоРасписанию;
	Иначе
		Если ЗначениеЗаполнено(ТекущийВариантПроверки) Тогда
			ВариантПроверки = ТекущийВариантПроверки;
		Иначе
			ВариантПроверки = Конфигурация.ВариантПроверкиВручную;
		КонецЕсли;
	КонецЕсли;
	
	КэшУстановленныхФлажков = РаботаСИнтерфейсом.ЗаполнитьТаблицуТребований(Конфигурация, ВариантПроверки);
	
	Если НЕ ФлагПроверкаПоРасписанию Тогда
		КэшУстановленныхФлажковЭлементы =
			РаботаСИнтерфейсом.ПолучитьЭлементыИзКэшаУстановленныхФлажковТребований(КэшУстановленныхФлажков);
		Для Каждого СтрокаКэшаУстановленныхФлажков Из КэшУстановленныхФлажковЭлементы.Строки Цикл
			РаботаСИнтерфейсом.УстановитьФлажокРодителюОбъекта(СтрокаКэшаУстановленныхФлажков.Ссылка,
				Справочники.Требования.ПустаяСсылка(), КэшУстановленныхФлажков.Строки);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеОбъектами

// Возвращает массив объектов на проверку. В массив попадают только объекты, выбранные на форме запуска проверки.
// Если на форме не выбран ни один объект, то по умолчанию будут проверены все объекты конфигурации.
//
Функция ПолучитьСоставОбъектовДляПроверки(ФлагПроверитьВсе, КэшУстановленныхФлажковСпискаОбъектов, СписокРасширений) Экспорт
	
	СоставОбъектовДляПроверки = Новый Массив;
	
	Если ФлагПроверитьВсе Тогда
		Возврат СоставОбъектовДляПроверки;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаКЭШ.Ссылка,
	|	ТаблицаКЭШ.Значение КАК Пометка
	|ПОМЕСТИТЬ ВТ_ТаблицаКЭШ
	|ИЗ
	|	&ТаблицаКЭШ КАК ТаблицаКЭШ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаКЭШ.Ссылка
	|ИЗ
	|	ВТ_ТаблицаКЭШ КАК ВТ_ТаблицаКЭШ
	|ГДЕ
	|	ВТ_ТаблицаКЭШ.Пометка = 1
	|	И ВТ_ТаблицаКЭШ.Ссылка.Расширение В(&СписокРасширений)";
	
	Запрос.УстановитьПараметр("ТаблицаКЭШ", КэшУстановленныхФлажковСпискаОбъектов);
	Запрос.УстановитьПараметр("СписокРасширений", СписокРасширений);
	Выборка = Запрос.Выполнить();
	Если НЕ Выборка.Пустой() Тогда
		СоставОбъектовДляПроверки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Возврат СоставОбъектовДляПроверки;
	
КонецФункции

Функция СопоставитьОбъектыДляПроверки(СоставОбъектовДляПроверки)
	
	Если НЕ ЗначениеЗаполнено(СоставОбъектовДляПроверки) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Если (НЕ ЗначениеЗаполнено(ИсходнаяВерсия)) ИЛИ (ИсходнаяВерсия = Версия) Тогда
		Возврат СоставОбъектовДляПроверки;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Ссылка КАК Ссылка,
	|	СтруктураКонфигурации.Путь КАК Путь
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", Версия);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("Путь");
	
	НовыйСоставОбъектовДляПроверки = Новый Массив;
	
	Для Каждого ОбъектДляПроверки Из СоставОбъектовДляПроверки Цикл
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
		ИсходныйОбъект = Результат.Найти(ОбъектДляПроверки.Путь, "Путь");
		
		Если ИсходныйОбъект <> Неопределено Тогда
			НовыйСоставОбъектовДляПроверки.Добавить(ИсходныйОбъект.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НовыйСоставОбъектовДляПроверки;
	
КонецФункции

Процедура ЗаполнитьКоличествоОбъектовДляПроверки(СоставОбъектовДляПроверки, Расширение = Неопределено,
	ВсеОбъекты = Истина) Экспорт
	
	КоличествоОбъектовВыборочно = ?(СоставОбъектовДляПроверки = Неопределено, 0, СоставОбъектовДляПроверки.Количество());
	КоличествоОбъектовОбщее = ПолучитьОбщееКоличествоОбъектовКонфигурации(Версия, Конфигурация,, Расширение, ВсеОбъекты);
	КоличествоОбъектовСобраныСведения = ПолучитьОбщееКоличествоОбъектовКонфигурации(Версия, Конфигурация, Истина,
		Расширение, ВсеОбъекты);
	
	// При проверке были выбраны конкретные объекты в дереве.
	Если КоличествоОбъектовВыборочно > 0 Тогда
		ПроверяемыеОбъекты = СтрШаблон(НСтр("ru='Выборочно (%1 из %2)'"), КоличествоОбъектовВыборочно,
			КоличествоОбъектовОбщее);
	ИначеЕсли КоличествоОбъектовСобраныСведения = КоличествоОбъектовОбщее Тогда
		// У конфигурации нет фильтров.
		ПроверяемыеОбъекты = СтрШаблон(НСтр("ru='Все объекты (%1)'"), КоличествоОбъектовОбщее);
	Иначе
		// У конфигурации настроены фильтры для объектов.
		ПроверяемыеОбъекты = СтрШаблон(НСтр("ru='Выборочно (%1 из %2)'"), КоличествоОбъектовСобраныСведения,
			КоличествоОбъектовОбщее);
	КонецЕсли;
	
	СтруктураПроверки.Вставить("ПроверяемыеОбъекты", ПроверяемыеОбъекты);
	
КонецПроцедуры

// Процедура, необходимая для создания таблицы значений, хранящей
// выбранные пользователем объекты в карточке запуска проверки.
//
Процедура ПрочитатьОбъекты(КэшУстановленныхФлажков) Экспорт
	
	Если КэшУстановленныхФлажков = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	СтруктураКонфигурации.Ссылка,
		|	СтруктураКонфигурации.Пометка КАК Значение
		|ИЗ
		|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
		|ГДЕ
		|	СтруктураКонфигурации.Владелец = &Владелец
		|	И СтруктураКонфигурации.Пометка <> 0";
		Запрос.УстановитьПараметр("Владелец", Версия);
		КэшУстановленныхФлажков = Запрос.Выполнить().Выгрузить();
		КэшУстановленныхФлажков.Индексы.Добавить("Ссылка");
		
	КонецЕсли;
	
КонецПроцедуры

// Меняет значение флажка у родителя требований в зависимости
// от нового значения флажка одного из его потомков.
//
Функция УстановитьФлажокРодителюОбъектаПроверки(Ссылка, ПустаяСсылка, Версия, КэшУстановленныхФлажков) Экспорт
	
	Родитель = Ссылка.Родитель;
	Если Родитель = ПустаяСсылка Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЭтоВеткаМетаданных = (Родитель.ТипОбъекта = Перечисления.ТипыОбъектов.ВеткаМетаданных);
	
	РодительПометка = НайтиВКэшеУстановленныхФлажков(КэшУстановленныхФлажков, Родитель);
	Если (НЕ ЭтоВеткаМетаданных) И (РодительПометка = 1) Тогда
		Возврат УстановитьФлажокРодителюОбъектаПроверки(Родитель, ПустаяСсылка, Версия, КэшУстановленныхФлажков);
	КонецЕсли;
	
	ЕстьНезаполненные = ПроверкаНезаполненныхФлажковОбъектовПроверки(КэшУстановленныхФлажков, Родитель);
	ЕстьЗаполненные = ПроверкаЗаполненностиФлажковОбъектовПроверки(КэшУстановленныхФлажков, Родитель);
	НовоеЗначение = ?(ЕстьНезаполненные, ?(ЕстьЗаполненные, 2, 0), ?(ЭтоВеткаМетаданных, 1, 2));
	Если (НЕ ЭтоВеткаМетаданных) И (НовоеЗначение = 1) Тогда
		Возврат УстановитьФлажокРодителюОбъектаПроверки(Родитель, ПустаяСсылка, Версия, КэшУстановленныхФлажков);
	КонецЕсли;
	
	ИзменитьКэшУстановленныхФлажков(КэшУстановленныхФлажков, Родитель, НовоеЗначение);
	
	Возврат УстановитьФлажокРодителюОбъектаПроверки(Родитель, ПустаяСсылка, Версия, КэшУстановленныхФлажков);
	
КонецФункции

// Меняет значение флажков потомков объектов в зависимости от нового значения флага родителя.
//
Функция УстановитьФлажкиПодчиненнымОбъектамПроверки(ТекущаяСсылка, НовоеЗначение, СтароеЗначение, Версия,
	КэшУстановленныхФлажков, ТипыОбъектов = Неопределено) Экспорт
	
	ТипыОбъектов = ?(ТипыОбъектов = Неопределено, ПолучитьТипыОбъектовРодителей(), ТипыОбъектов);
	Если ТипыОбъектов.Найти(ТекущаяСсылка.ТипОбъекта) = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МассивОбъектов = ПолучитьМассивПодчиненныхОбъектовПроверки(ТекущаяСсылка);
	Для Каждого Объект Из МассивОбъектов Цикл
		Ссылка = Объект.Ссылка;
		
		ИзменитьКэшУстановленныхФлажков(КэшУстановленныхФлажков, Ссылка, НовоеЗначение);
		
		Если Объект.ТипОбъекта = Перечисления.ТипыОбъектов.Подсистема Тогда
			УстановитьФлажкиОбъектамПроверкиПодсистемы(Ссылка, НовоеЗначение, СтароеЗначение, Версия, КэшУстановленныхФлажков);
		КонецЕсли;
		
		УстановитьФлажкиПодчиненнымОбъектамПроверки(Ссылка, НовоеЗначение, СтароеЗначение, Версия,
			КэшУстановленныхФлажков, ТипыОбъектов);
		
	КонецЦикла;
	
КонецФункции

// Устанавливает флажки объектам конфигурации.
// Параметры:
//   ТекущаяСсылка - Ссылка на элемент конфигурации.
//   НовоеЗначение - Устанавливаемое значение.
//   СтароеЗначение - Старое значение.
//   Версия - Версия конфигурации.
//
Функция УстановитьФлажкиОбъектамПроверки(ТекущаяСсылка, НовоеЗначение, СтароеЗначение, Версия,
	КэшУстановленныхФлажков) Экспорт
	
	ФлагВыбораВсех = (ТекущаяСсылка = Справочники.СтруктураКонфигурации.ПустаяСсылка());
	
	Если НЕ ФлагВыбораВсех Тогда
		
		ИзменитьКэшУстановленныхФлажков(КэшУстановленныхФлажков, ТекущаяСсылка, НовоеЗначение);
		
		Если ТекущаяСсылка.ТипОбъекта = Перечисления.ТипыОбъектов.Подсистема Тогда
			УстановитьФлажкиОбъектамПроверкиПодсистемы(ТекущаяСсылка, НовоеЗначение, СтароеЗначение, Версия,
				КэшУстановленныхФлажков);
		КонецЕсли;
		
	КонецЕсли;
	
	ТипыОбъектов = ПолучитьТипыОбъектовРодителей();
	
	УстановитьФлажкиПодчиненнымОбъектамПроверки(ТекущаяСсылка, НовоеЗначение, СтароеЗначение, Версия,
		КэшУстановленныхФлажков, ТипыОбъектов);
	
	Если НЕ ФлагВыбораВсех Тогда
		
		УстановитьФлажокРодителюОбъектаПроверки(ТекущаяСсылка, Справочники.СтруктураКонфигурации.ПустаяСсылка(), Версия,
			КэшУстановленныхФлажков);
		
	КонецЕсли;
	
КонецФункции

// Устанавливает флажки объектам подсистемы.
// Параметры:
//   Подсистема - Ссылка на элемент конфигурации.
//   НовоеЗначение - Устанавливаемое значение.
//   СтароеЗначение - Старое значение.
//   Версия - Версия конфигурации.
//   КэшУстановленныхФлажков - Кэш установленных флажков.
//
Функция УстановитьФлажкиОбъектамПроверкиПодсистемы(Подсистема, НовоеЗначение, СтароеЗначение, Версия,
	КэшУстановленныхФлажков) Экспорт
	
	ТаблицаОбъектов = ПолучитьТаблицуОбъектовПроверкиПодсистемы(Подсистема, Версия);
	Для Каждого Объект Из ТаблицаОбъектов Цикл
		
		УстановитьФлажкиОбъектамПроверки(Объект.Ссылка, НовоеЗначение, СтароеЗначение, Версия, КэшУстановленныхФлажков);
		УстановитьФлажокРодителюОбъектаПроверки(Объект.Ссылка, Справочники.СтруктураКонфигурации.ПустаяСсылка(), Версия,
			КэшУстановленныхФлажков);
		
	КонецЦикла;
	
КонецФункции

// Получает объекты всех уровней, входящих в переданную подсистему.
//
Функция ПолучитьТаблицуОбъектовПроверкиПодсистемы(Подсистема, Версия)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаОбъектовКонфигурации.Ссылка
	|ПОМЕСТИТЬ ВТ_ТаблицаОбъектовКонфигурации
	|ИЗ
	|	&ТаблицаОбъектовКонфигурации КАК ТаблицаОбъектовКонфигурации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаОбъектовКонфигурации.Ссылка
	|ИЗ
	|	ВТ_ТаблицаОбъектовКонфигурации КАК ВТ_ТаблицаОбъектовКонфигурации
	|ГДЕ
	|	ВТ_ТаблицаОбъектовКонфигурации.Ссылка.Подсистемы.Подсистема = &Подсистема
	|	И ВТ_ТаблицаОбъектовКонфигурации.Ссылка.ТипОбъекта <> &ТипОбъекта";
	
	Запрос.УстановитьПараметр("ТаблицаОбъектовКонфигурации", ТаблицаОбъектовКонфигурации);
	Запрос.УстановитьПараметр("Подсистема", Подсистема);
	Запрос.УстановитьПараметр("ТипОбъекта", Перечисления.ТипыОбъектов.ВеткаМетаданных);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьМассивПодчиненныхОбъектовПроверки(Родитель)
	
	Возврат ТаблицаОбъектовКонфигурации.НайтиСтроки(Новый Структура("Родитель", Родитель));
	
КонецФункции

// Проверяет значение заполненных флагов.
//
Функция ПроверкаЗаполненностиФлажковОбъектовПроверки(КэшУстановленныхФлажков, Ссылка) Экспорт
	
	МассивОбъектов = ПолучитьМассивПодчиненныхОбъектовПроверки(Ссылка);
	Для Каждого Объект Из МассивОбъектов Цикл
		
		ЗначениеФлажка = НайтиВКэшеУстановленныхФлажков(КэшУстановленныхФлажков, Объект.Ссылка);
		Если ЗначениеФлажка > 0 Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет значение незаполненных флагов.
//
Функция ПроверкаНезаполненныхФлажковОбъектовПроверки(КэшУстановленныхФлажков, Ссылка)
	
	МассивОбъектов = ПолучитьМассивПодчиненныхОбъектовПроверки(Ссылка);
	Для Каждого Объект Из МассивОбъектов Цикл
		
		ЗначениеФлажка = НайтиВКэшеУстановленныхФлажков(КэшУстановленныхФлажков, Объект.Ссылка);
		Если ЗначениеФлажка <> 1 Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область УправлениеДокументомПроверкаВерсии

// Проверяет последний документ проверки версии.
// Если у документа статус "Выполняется", то устанавливает статус "Ошибка"
// и дописывает в журнал проверки "Вероятно, проверка была прервана пользователем".
//
Процедура ПроверитьПоследнийДокументПроверкиВерсии()
	
	ЗапросДокументы = Новый Запрос;
	ЗапросДокументы.Текст = "
	|ВЫБРАТЬ
	|	ПроверкаВерсии.Ссылка
	|ИЗ
	|	Документ.ПроверкаВерсии КАК ПроверкаВерсии
	|ГДЕ
	|	ПроверкаВерсии.Конфигурация = &Конфигурация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПроверкаВерсии.Дата УБЫВ";
	
	ЗапросДокументы.УстановитьПараметр("Конфигурация", Конфигурация);
	Выборка = ЗапросДокументы.Выполнить().Выбрать();
	
	Если НЕ Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = Выборка.Ссылка;
	Если СтрСравнить(ДокументСсылка.Статус, "Выполняется") <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru='Вероятно, проверка была прервана пользователем.'");
	
	ЖурналПроверки = ДокументСсылка.ЖурналПроверки;
	ЖурналПроверки = СтрШаблон("%1%2%3", ЖурналПроверки, Символы.ПС, ТекстОшибки);
	
	Попытка
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		ДокументОбъект.Статус = "Ошибка";
		ДокументОбъект.ЖурналПроверки = ЖурналПроверки;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

Процедура СоздатьДокументПроверкиВерсии() Экспорт
	
	ПроверитьПоследнийДокументПроверкиВерсии();
	
	ИнициализироватьСтруктуруПроверки();
	
	СтруктураПроверки.ВремяНачала = ТекущаяДатаСеанса();
	СтруктураПроверки.РегистрироватьВсеОшибкиКакОсобенности = РегистрироватьВсеОшибкиКакОсобенности;
	
	ДокументПроверки = Документы.ПроверкаВерсии.СоздатьДокумент();
	ДокументПроверки.Дата = ТекущаяДатаСеанса();
	ДокументПроверки.УстановитьНовыйНомер();
	ДокументПроверки.РегистрироватьВсеОшибкиКакОсобенности = РегистрироватьВсеОшибкиКакОсобенности;
	
	Если ЗначениеЗаполнено(Версия) Тогда
		ДокументПроверки.ВерсияКонфигурации = Версия;
		ДокументПроверки.Эталон = Версия;
	КонецЕсли;
	
	ДокументПроверки.Записать(РежимЗаписиДокумента.Запись);
	ПроверкаВерсии = ДокументПроверки.Ссылка;
	
	Текст = НСтр("ru='Создан документ'") + " " + ПроверкаВерсии;
	Зафиксировать(Конфигурация.Наименование, УровеньИнформация, Текст, СтруктураПроверки.ЖурналПроверки,
		СтруктураПроверки.ТаблицаЖурнала);
	
КонецПроцедуры

Процедура ЗаписатьДокументПроверкиВерсии(СтатусПроверки = "", СоздаватьВерсию = Истина) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПроверкаВерсии) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроверки = ПроверкаВерсии.ПолучитьОбъект();
	
	Если СоздаватьВерсию Тогда
		Если НЕ ЗначениеЗаполнено(ДокументПроверки.ВерсияКонфигурации) Тогда
			НоваяВерсия = Справочники.Версии.СоздатьЭлемент();
			НоваяВерсия.Владелец = Конфигурация;
			НоваяВерсия.Наименование = НСтр("ru='Не определена'");
			НоваяВерсия.Код = "0.0.0";
			НоваяВерсия.Записать();
			
			ДокументПроверки.ВерсияКонфигурации = НоваяВерсия.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПроверки.ВремяОкончания = ТекущаяДатаСеанса();
	ОбщееВремя = СтруктураПроверки.ВремяОкончания - СтруктураПроверки.ВремяНачала;
	СтруктураПроверки.ОбщееВремя = ПолучитьВремяВФормате(ОбщееВремя);
	СтруктураПроверки.Статус = ?(ПустаяСтрока(СтатусПроверки), "Выполняется", СтатусПроверки);
	
	ЗаполнитьЗначенияСвойств(ДокументПроверки, СтруктураПроверки);
	
	ДокументПроверки.Записать(РежимЗаписиДокумента.Запись);
	
КонецПроцедуры

Процедура ЗаполнитьСоставПроверки(ПараметрыПроверки, СоставОбъектовДляПроверки)
	
	Если НЕ ЗначениеЗаполнено(ПроверкаВерсии) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаТребований = ПараметрыПроверки.ТаблицаТребований;
	
	ДокументПроверки = ПроверкаВерсии.ПолучитьОбъект();
	ДокументПроверки.СоставТребований.Загрузить(ТаблицаТребований);
	
	ТаблицаПравил = ПолучитьТаблицуПравилСХешСуммами(ТаблицаТребований);
	ДокументПроверки.СоставПравил.Загрузить(ТаблицаПравил);
	
	СоставОбъектовДляПроверки = СопоставитьОбъектыДляПроверки(СоставОбъектовДляПроверки);
	
	Для Каждого ОбъектДляПроверки Из СоставОбъектовДляПроверки Цикл
		НовыйОбъектДляПроверки = ДокументПроверки.СоставОбъектов.Добавить();
		НовыйОбъектДляПроверки.Объект = ОбъектДляПроверки;
	КонецЦикла;
	
	ДокументПроверки.Записать(РежимЗаписиДокумента.Запись);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру с заполненными параметрами проверки.
//
Функция ЗаполнитьПараметрыПроверки(ЭтапыПроверкиПоТребованиям = Неопределено,
	ПроверитьПлатформуДляЗапускаПроверки = Истина,
	ПроверитьСоединениеСБазой = Истина,
	ЗагрузитьКонфигурациюИзХранилища = Истина,
	НастройкиХранилища = Неопределено,
	ПроверитьВерсиюКонфигурации = Истина,
	ОпределятьРежимЗапуска = Истина,
	ОчищатьСобранныеДанные = Истина,
	ПостроитьСтруктуру = Истина,
	ПроверитьКонфигурацию = Истина,
	СписокТребований = Неопределено,
	ТаблицаТребований = Неопределено) Экспорт
	
	Если ЭтапыПроверкиПоТребованиям = Неопределено Тогда
		ЭтапыПроверкиПоТребованиям = ПолучитьЭтапыПроверкиПоТребованиям();
	КонецЕсли;
	
	Если НастройкиХранилища = Неопределено Тогда
		НастройкиХранилища = Новый Структура;
	КонецЕсли;
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ПроверитьПлатформуДляЗапускаПроверки", ПроверитьПлатформуДляЗапускаПроверки);
	ПараметрыПроверки.Вставить("ПроверитьСоединениеСБазой", ПроверитьСоединениеСБазой);
	ПараметрыПроверки.Вставить("ЗагрузитьКонфигурациюИзХранилища", ЗагрузитьКонфигурациюИзХранилища);
	ПараметрыПроверки.Вставить("НастройкиХранилища", НастройкиХранилища);
	ПараметрыПроверки.Вставить("ПроверитьВерсиюКонфигурации", ПроверитьВерсиюКонфигурации);
	ПараметрыПроверки.Вставить("ОпределятьРежимЗапуска", ОпределятьРежимЗапуска);
	ПараметрыПроверки.Вставить("ОчищатьСобранныеДанные", ОчищатьСобранныеДанные);
	ПараметрыПроверки.Вставить("ПостроитьСтруктуру", ПостроитьСтруктуру);
	ПараметрыПроверки.Вставить("ПроверитьКонфигурацию", ПроверитьКонфигурацию);
	ПараметрыПроверки.Вставить("ЭтапыПроверкиПоТребованиям", ЭтапыПроверкиПоТребованиям);
	ПараметрыПроверки.Вставить("СписокТребований", СписокТребований);
	ПараметрыПроверки.Вставить("ТаблицаТребований", ТаблицаТребований);
	
	Возврат ПараметрыПроверки;
	
КонецФункции

Процедура ВерсияУстановитьФлагиСбораДанных(ЗначениеФлага = Истина, Расширение = Неопределено)
	
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанные", ЗначениеФлага, Расширение);
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОМетаданных", ЗначениеФлага, Расширение);
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОФормах", ЗначениеФлага, Расширение);
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОМодулях", ЗначениеФлага, Расширение);
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОРолях", ЗначениеФлага, Расширение);
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОМакетах", ЗначениеФлага, Расширение);
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеОСправке", ЗначениеФлага, Расширение);
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеПоПроверкеКонфигурации", ЗначениеФлага, Расширение);
	ВерсияУстановитьФлагСбораДанных(Версия, "СобраныДанныеПоПроверкеПримененияРасширения", ЗначениеФлага, Расширение);
	
КонецПроцедуры

Функция ПолучитьМассивИсключенийОшибокВыгрузки()
	
	МассивИсключений = Новый Массив;
	МассивИсключений.Добавить("Конфигурация содержит объекты метаданных, длина имен которых превышает 80 символов!");
	
	Возврат МассивИсключений;
	
КонецФункции

// Возвращает информацию о настройках проверки конфигурации.
// Если не выбран ни один объект конфигурации, то по умолчанию
// будут проверены все объекты конфигурации.
// Если не указано ни одно требование, то по умолчанию
// конфигурация будет проверяться по всем требованиям.
//
Функция ПолучитьТекстОписанияНастроек(НомерПроверки, КэшУстановленныхФлажков, СоставОбъектовДляПроверки,
	ФлажокЗагрузитьКонфигурациюИзХранилища, КаталогХранилища, ФлажокВыполнятьРассылкуРезультатовПроверки,
	Сценарий, СписокРасширений) Экспорт
	
	КоличествоОбъектовДляПроверки = СоставОбъектовДляПроверки.Количество();
	Если КэшУстановленныхФлажков.Количество() = 0 Тогда
		КоличествоТребований = 0;
	Иначе
		КоличествоТребований = ПолучитьСписокТребований(КэшУстановленныхФлажков, НомерПроверки).Количество();
	КонецЕсли;
	
	Если НомерПроверки = 1 Тогда
		
		ПользовательТекст = ?(ЗначениеЗаполнено(Пользователь), Пользователь, НСтр("ru='<не задан>'"));
		ПарольТекст = ?(ЗначениеЗаполнено(Пароль), Пароль, НСтр("ru='<не задан>'"));
		
		Текст = СтрШаблон(НСтр("ru='Каталог: %1;%4Пользователь: %2; Пароль: %3%4'"),
			КаталогКонфигурации, ПользовательТекст, ПарольТекст, Символы.ПС);
		
	ИначеЕсли НомерПроверки = 2 Тогда
		
		ВерсияТекст = ?(ЗначениеЗаполнено(Версия), Версия, НСтр("ru='<не указана>'"));
		Текст = СтрШаблон(НСтр("ru='Версия на проверку: %1'"), ВерсияТекст) + Символы.ПС;
		
	КонецЕсли;
	
	КоличествоОбъектовТекст = ?(КоличествоОбъектовДляПроверки = 0, НСтр("ru='все'"), КоличествоОбъектовДляПроверки);
	КоличествоТребованийТекст = ?(КоличествоТребований = 0, НСтр("ru='все'"), КоличествоТребований);
	
	Текст = Текст + СтрШаблон(НСтр("ru='Проверяемые объекты (%1), проверяемые требования (%2)'"),
		КоличествоОбъектовТекст, КоличествоТребованийТекст) + Символы.ПС;
		
	Если Сценарий = 0 Тогда
		ТекстСценария = НСтр("ru='Конфигурация'");
	Иначе
		КоличествоРасширений = СписокРасширений.Количество();
		Если Сценарий = 1 Тогда
			ТекстСценария = НСтр("ru='Расширения (%1)'");
		Иначе
			ТекстСценария = НСтр("ru='Конфигурация и расширения (%1)'");
			КоличествоРасширений = КоличествоРасширений - 1;
		КонецЕсли;
		ТекстСценария = СтрШаблон(ТекстСценария, КоличествоРасширений);
	КонецЕсли;
		
	Текст = Текст + СтрШаблон(НСтр("ru='Сценарий: %1'"), ТекстСценария);
		
	Если (НомерПроверки = 1) И ФлажокЗагрузитьКонфигурациюИзХранилища Тогда
		Текст = Текст + ?(ЗначениеЗаполнено(КаталогХранилища), Символы.ПС
			+ НСтр("ru='Перед проверкой будет выполнена загрузка конфигурации из хранилища:'") + Символы.ПС
			+ КаталогХранилища, "");
	КонецЕсли;
	
	Если (НомерПроверки < 3) И ФлажокВыполнятьРассылкуРезультатовПроверки Тогда
		Текст = Текст + Символы.ПС + НСтр("ru='После проверки будут отправлены отчеты ответственным на почту.'");
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

// Создает массив и заполняет его типами объектов, которые могут иметь подчиненные объекты.
// Возвращаемое значение:
//   Массив, заполненный типами объектов.
//
Функция ПолучитьТипыОбъектовРодителей() Экспорт
	
	ПеречисленияТипы = Перечисления.ТипыОбъектов;
	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(ПеречисленияТипы.БизнесПроцесс);
	ТипыОбъектов.Добавить(ПеречисленияТипы.Документ);
	ТипыОбъектов.Добавить(ПеречисленияТипы.ЖурналДокументов);
	ТипыОбъектов.Добавить(ПеречисленияТипы.Задача);
	ТипыОбъектов.Добавить(ПеречисленияТипы.КритерийОтбора);
	ТипыОбъектов.Добавить(ПеречисленияТипы.Обработка);
	ТипыОбъектов.Добавить(ПеречисленияТипы.Отчет);
	ТипыОбъектов.Добавить(ПеречисленияТипы.Перечисление);
	ТипыОбъектов.Добавить(ПеречисленияТипы.ПланВидовРасчета);
	ТипыОбъектов.Добавить(ПеречисленияТипы.ПланВидовХарактеристик);
	ТипыОбъектов.Добавить(ПеречисленияТипы.ПланОбмена);
	ТипыОбъектов.Добавить(ПеречисленияТипы.ПланСчетов);
	ТипыОбъектов.Добавить(ПеречисленияТипы.Подсистема);
	ТипыОбъектов.Добавить(ПеречисленияТипы.РегистрБухгалтерии);
	ТипыОбъектов.Добавить(ПеречисленияТипы.РегистрНакопления);
	ТипыОбъектов.Добавить(ПеречисленияТипы.РегистрРасчета);
	ТипыОбъектов.Добавить(ПеречисленияТипы.РегистрСведений);
	ТипыОбъектов.Добавить(ПеречисленияТипы.Справочник);
	ТипыОбъектов.Добавить(ПеречисленияТипы.ХранилищеНастроек);
	
	ТипыОбъектов.Добавить(ПеречисленияТипы.ВеткаМетаданных);
	ТипыОбъектов.Добавить(ПеречисленияТипы.ТабличнаяЧасть);
	
	ТипыОбъектов.Добавить(ПеречисленияТипы.ПустаяСсылка());
	
	Возврат ТипыОбъектов;
	
КонецФункции

// Инициализирует журнал проверки и устанавливает время начала проверки.
//
Функция ИнициализироватьСтруктуруПроверки()
	
	СтруктураПроверки = Новый Структура;
	СтруктураПроверки.Вставить("ЖурналПроверки", "");
	СтруктураПроверки.Вставить("ВремяНачала");
	СтруктураПроверки.Вставить("ВремяОкончания");
	СтруктураПроверки.Вставить("ОбщееВремя");
	СтруктураПроверки.Вставить("ОбнаруженоОшибок");
	СтруктураПроверки.Вставить("ВариантПроверки");
	СтруктураПроверки.Вставить("ПроверяемыеОбъекты");
	СтруктураПроверки.Вставить("Статус");
	СтруктураПроверки.Вставить("ВременныеФайлы", Новый Массив);
	СтруктураПроверки.Вставить("РегистрироватьВсеОшибкиКакОсобенности");
	СтруктураПроверки.Вставить("ТаблицаЖурнала", Проверка.ИнициализироватьТаблицуЖурналаПроверки());
	
КонецФункции

// Копирует исходную проверяемую базу в каталог временных файлов.
//
Процедура СоздатьКопиюКонфигурации() Экспорт
	
	КаталогКонфигурации = ОтредактироватьПутьККаталогу(КаталогКонфигурации);
	
	// Если выбрана проверка со сбором сведений, то скопируем проверяемую базу в каталог временных файлов.
	КаталогКонфигурацииКопия = ПолучитьКаталогВременныхФайлов();
	
	ТекстОшибки = КопироватьБазу(КаталогКонфигурации, КаталогКонфигурацииКопия);
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		// Если не удалось скопировать проверяемую базу из указанного каталога на форме,
		// то скопируем ее из каталога конфигурации.
		ТекстОшибки = КопироватьБазу(Конфигурация.КаталогКонфигурации, КаталогКонфигурацииКопия);
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	// Не смогли скопировать проверяемую базу ни из каталога с формы обработки, ни из карточки конфигурации.
	// Переустановим каталог копии базы.
	Если ДемоБазаСуществует(КаталогКонфигурации) Тогда
		КаталогКонфигурацииКопия = КаталогКонфигурации;
	ИначеЕсли ДемоБазаСуществует(Конфигурация.КаталогКонфигурации) Тогда
		КаталогКонфигурацииКопия = Конфигурация.КаталогКонфигурации;
	Иначе
		КаталогКонфигурацииКопия = "";
	КонецЕсли;
	
КонецПроцедуры

// Удаляет все временные файлы и каталоги из массива.
//
Процедура ОчиститьМассивВременныхФайлов() Экспорт
	
	МассивУдаленныхФайлов = Новый Массив;
	
	// Удаляем все временные файлы и каталоги.
	Для Каждого ВременныйФайл Из СтруктураПроверки.ВременныеФайлы Цикл
		
		// Каталог исходной конфигурации удалять не надо.
		Если СтрСравнить(КаталогКонфигурации, ВременныйФайл) = 0
		 ИЛИ СтрСравнить(Конфигурация.КаталогКонфигурации, ВременныйФайл) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ФайлУдалить(ВременныйФайл);
		Если НЕ ФайлСуществует(ВременныйФайл) Тогда
			МассивУдаленныхФайлов.Добавить(ВременныйФайл);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого УдаленныйФайл Из МассивУдаленныхФайлов Цикл
		
		ИндексФайла = СтруктураПроверки.ВременныеФайлы.Найти(УдаленныйФайл);
		Если ИндексФайла <> Неопределено Тогда
			СтруктураПроверки.ВременныеФайлы.Удалить(ИндексФайла);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Функция получает последнюю версию хранилища из документов проверки.
//
Функция ПолучитьВерсиюХранилищаИзДокументовПроверки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПроверкаВерсии.ВерсияХранилища КАК ВерсияХранилища
	|ИЗ
	|	Документ.ПроверкаВерсии КАК ПроверкаВерсии
	|ГДЕ
	|	ПроверкаВерсии.Конфигурация = &Конфигурация
	|	И ПроверкаВерсии.Статус <> &Статус
	|	И НЕ ПроверкаВерсии.ПометкаУдаления
	|	И ПроверкаВерсии.Ссылка <> &ТекущийДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПроверкаВерсии.Дата УБЫВ";

	Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
	Запрос.УстановитьПараметр("Статус", "Ошибка");
	Запрос.УстановитьПараметр("ТекущийДокумент", ПроверкаВерсии);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.ВерсияХранилища;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

Процедура ДобавитьОбъектыВСоответствиеВключенныхВПроверкуОбъектов(СоответствиеДляДобавления)
	
	Если СоответствиеДляДобавления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СоответствиеВключенныхВПроверкуОбъектов = Неопределено Тогда
		СоответствиеВключенныхВПроверкуОбъектов = СоответствиеДляДобавления;
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементСоответствия Из СоответствиеДляДобавления Цикл
		СоответствиеВключенныхВПроверкуОбъектов.Вставить(ЭлементСоответствия.Ключ, Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствиеПроверяемыхОбъектов()
	
	Если СоответствиеВключенныхВПроверкуОбъектов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НайденныеОшибки.Объект.Путь КАК ОбъектПуть
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|ГДЕ
	|	НайденныеОшибки.Объект.Владелец = &Владелец
	|	И НайденныеОшибки.Состояние = &Состояние";
	
	Запрос.УстановитьПараметр("Владелец", Версия);
	Запрос.УстановитьПараметр("Состояние", Перечисления.СостояниеОшибки.Исправлена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СоответствиеВключенныхВПроверкуОбъектов.Вставить(Выборка.ОбъектПуть, Истина);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТаблицуПравилСХешСуммами(ТаблицаТребований)
	
	ТаблицаТребований.Свернуть("Требование");
	Требования = ТаблицаТребований.ВыгрузитьКолонку("Требование");
	
	ПравилаПроверки = ПолучитьПравилаПоСпискуТребований(Требования);
	
	ТаблицаПравилСХешСуммой = Новый ТаблицаЗначений;
	ТаблицаПравилСХешСуммой.Колонки.Добавить("Правило");
	ТаблицаПравилСХешСуммой.Колонки.Добавить("ХешСумма");
	
	Для Каждого Правило Из ПравилаПроверки Цикл
		
		ОбслуживаемыеТипы = Правило.ОбслуживаемыеТипы;
		СтрокаТиповПроверяемыхОбъектов = "";
		Для Каждого ОбслуживаемыйТип Из ОбслуживаемыеТипы Цикл
			СтрокаТиповПроверяемыхОбъектов = СтрокаТиповПроверяемыхОбъектов + ОбслуживаемыйТип.ТипОбъекта;
		КонецЦикла;
		
		СтрокаДляХеширования = Правило.Алгоритм + СтрокаТиповПроверяемыхОбъектов;
		
		ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
		ХешированиеДанных.Добавить(СтрокаДляХеширования);
		ХешСумма = СтрЗаменить(Строка(ХешированиеДанных.ХешСумма), " ", "");
		
		НоваяСтрока = ТаблицаПравилСХешСуммой.Добавить();
		НоваяСтрока.Правило = Правило;
		НоваяСтрока.ХешСумма = ХешСумма;
	КонецЦикла;
	
	Возврат ТаблицаПравилСХешСуммой;
	
КонецФункции

#КонецОбласти

#Область Инициализация

ИнициализироватьСтруктуруПроверки();
УровеньИнформация = УровеньЖурналаРегистрации.Информация;
УровеньОшибка = УровеньЖурналаРегистрации.Ошибка;

ФайлОсобенностей = "";
СоответствиеВключенныхВПроверкуОбъектов = Неопределено;
ПоказыватьПредупреждения = Истина;

#КонецОбласти

#КонецЕсли