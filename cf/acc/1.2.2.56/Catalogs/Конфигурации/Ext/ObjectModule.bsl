#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ЭтоКопия Экспорт;    // Флаг копирования объекта.
Перем ОбъектКопия Экспорт; // Содержит ссылку на источник копирования.

#КонецОбласти

#Область ОбработчикиСобытий

// Обработчик ПередЗаписью
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтоКопия = Истина;
	ОбъектКопия = ОбъектКопирования.Ссылка;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоКопия Тогда
		СкопироватьКонфигурацию(ОбъектКопия, Ссылка);
		ЭтоКопия = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КопированиеКонфигурации

Процедура СкопироватьКонфигурацию(Источник, Приемник)
	
	ВерсияИсточник = НайтиПоследнююВерсию(Источник, Ложь);
	Если ЗначениеЗаполнено(ВерсияИсточник) Тогда
		ВерсияПриемник = СкопироватьВерсиюКонфигурации(Источник, Приемник, ВерсияИсточник);
		СкопироватьСтруктуруКонфигурации(ВерсияИсточник, ВерсияПриемник);
		
		СкопироватьРасширенияКонфигурации(ВерсияИсточник, ВерсияПриемник);
		
		ТаблицаНомеровОшибок = СкопироватьОшибкиКонфигурации(ВерсияИсточник, ВерсияПриемник);
		СкопироватьКомментарииНайденныхОшибок(ТаблицаНомеровОшибок);
		
	Иначе
		Сообщить(НСтр("ru='Не найдено ни одной версии конфигурации-источника.'") + " "
			+ НСтр("ru='Версия и структура конфигурации не будут скопированы.'"));
	КонецЕсли;
	
КонецПроцедуры

Функция СкопироватьВерсиюКонфигурации(Источник, Приемник, ВерсияИсточник)
	
	МассивСвойствДляИсключения = Новый Массив;
	МассивСвойствДляИсключения.Добавить("Владелец");
	МассивСвойствДляИсключения.Добавить("Родитель");
	
	СвойстваДляИсключения = СтрСоединить(МассивСвойствДляИсключения, ", ");
	
	НовыйЭлемент = Справочники.Версии.СоздатьЭлемент();
	НовыйЭлемент.Владелец = Приемник;
	ЗаполнитьЗначенияСвойств(НовыйЭлемент, ВерсияИсточник,, СвойстваДляИсключения);
	
	Для Каждого СтрокаСобранныеДанные Из ВерсияИсточник.СобранныеДанные Цикл
		ЗаполнитьЗначенияСвойств(НовыйЭлемент.СобранныеДанные.Добавить(), СтрокаСобранныеДанные);
	КонецЦикла;
	
	НовыйЭлемент.Записать();
	
	Возврат НовыйЭлемент.Ссылка;
	
КонецФункции

Процедура СкопироватьРасширенияКонфигурации(ВерсияИсточник, ВерсияПриемник)
	
	Расширения.Очистить();
	
	МассивСвойствДляИсключения = Новый Массив;
	МассивСвойствДляИсключения.Добавить("Владелец");
	МассивСвойствДляИсключения.Добавить("Родитель");
	
	Источник = ВерсияИсточник.Владелец;
	Приемник = ВерсияПриемник.Владелец;
	
	СвойстваДляИсключения = СтрСоединить(МассивСвойствДляИсключения, ", ");
	
	Выборка = Справочники.Расширения.Выбрать(, Источник);
	Пока Выборка.Следующий() Цикл
		
		РасширениеИсточник = Выборка.Ссылка;
		
		Если РасширениеИсточник.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйЭлемент = Справочники.Расширения.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, РасширениеИсточник,, СвойстваДляИсключения);
		НовыйЭлемент.Владелец = Приемник;
		НовыйЭлемент.Наименование = РасширениеИсточник.Наименование;
		НовыйЭлемент.Записать();
		
		Если Приемник.Расширения.Найти(РасширениеИсточник) <> Неопределено Тогда
			НоваяСтрока = Расширения.Добавить();
			НоваяСтрока.Расширение = НовыйЭлемент.Ссылка;
		КонецЕсли;
		
		СкопироватьСтруктуруКонфигурации(ВерсияИсточник, ВерсияПриемник, РасширениеИсточник, НовыйЭлемент.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СкопироватьПодсистемыОбъекта(ОбъектИсточник, ОбъектПриемник)
	
	Для Каждого ПодсистемаИсточник Из ОбъектИсточник.Подсистемы Цикл
		
		ПодсистемаПуть = ПодсистемаИсточник.Подсистема.Путь;
		ВерсияПриемник = ОбъектПриемник.Владелец;
		ПодсистемаСсылка = Справочники.СтруктураКонфигурации.НайтиПоРеквизиту("Путь", ПодсистемаПуть,, ВерсияПриемник);
		
		ПодсистемаПриемник = ОбъектПриемник.Подсистемы.Добавить();
		ПодсистемаПриемник.Подсистема = ПодсистемаСсылка;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СкопироватьСтруктуруКонфигурации(ВерсияИсточник, ВерсияПриемник, РасширениеИсточник = Неопределено,
	РасширениеПриемник = Неопределено)
	
	РасширениеПриемникСсылка = ПолучитьСсылкуРасширения(РасширениеПриемник);
	РасширениеИсточникСсылка = ПолучитьСсылкуРасширения(РасширениеИсточник);
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(РасширениеИсточникСсылка);
	
	ЗапросПоОбъектам = Новый Запрос;
	ЗапросПоОбъектам.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Ссылка
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Владелец
	|	И СтруктураКонфигурации.Расширение = &Расширение
	|	И НЕ СтруктураКонфигурации.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтруктураКонфигурации.НомерПоПорядку";
		
	ЗапросПоОбъектам.УстановитьПараметр("Владелец", ВерсияИсточник);
	ЗапросПоОбъектам.УстановитьПараметр("Расширение", РасширениеИсточникСсылка);
	
	Выборка = ЗапросПоОбъектам.Выполнить().Выбрать();
	НомерОбъекта = 0;
	ВсегоОбъектов = Выборка.Количество();
	
	ШаблонаТекстаСостояния = СтрШаблон(НСтр("ru='Выполняется запись структуры %1'"), ОписаниеКонфигурацииИлиРасширения);;
	ШаблонаТекстаСостояния = ШаблонаТекстаСостояния + " (%1%%)";
	
	#Если Клиент Тогда
	Состояние(СтрШаблон(ШаблонаТекстаСостояния, "0"));
	#КонецЕсли
	
	ПроцентОбработанныхОбъектов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		СсылкаИсточник = Выборка.Ссылка;
		ОбъектПриемник = Справочники.СтруктураКонфигурации.СоздатьЭлемент();
		
		ЗаполнитьЗначенияСвойств(ОбъектПриемник, СсылкаИсточник,, "Владелец, Родитель, Код, Расширение");
		
		РодительПуть = Выборка.Ссылка.Родитель.Путь;
		РодительСсылка = Справочники.СтруктураКонфигурации.НайтиПоРеквизиту("Путь", РодительПуть,, ВерсияПриемник);
		ОбъектПриемник.Родитель = РодительСсылка;
		ОбъектПриемник.Владелец = ВерсияПриемник;
		ОбъектПриемник.Расширение = РасширениеПриемникСсылка;
		
		СкопироватьПодсистемыОбъекта(СсылкаИсточник, ОбъектПриемник);
		
		ЭлементЗаписан = Ложь;
		СчетчикТранзакций = 1;
		
		Пока (НЕ ЭлементЗаписан) И (СчетчикТранзакций < 1000) Цикл
			ЭлементЗаписан = Истина;
			Попытка
				ОбъектПриемник.Записать();
			Исключение
				ЭлементЗаписан = Ложь;
				Сообщить("Ошибка!");
			КонецПопытки;
			СчетчикТранзакций = СчетчикТранзакций + 1;
		КонецЦикла;
		
		НомерОбъекта = НомерОбъекта + 1;
		
		#Если Клиент Тогда
		ТекущийПроцентОбъектов = Цел(100 * НомерОбъекта / ВсегоОбъектов);
		
		Если ТекущийПроцентОбъектов > ПроцентОбработанныхОбъектов Тогда
			ПроцентОбработанныхОбъектов = ТекущийПроцентОбъектов;
			ТекстСостояния = СтрШаблон(ШаблонаТекстаСостояния, Строка(ТекущийПроцентОбъектов));
			Состояние(ТекстСостояния);
		КонецЕсли;
		#КонецЕсли
		
	КонецЦикла;
	
	#Если Клиент Тогда
	Состояние("");
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Инициализация

ЭтоКопия = Ложь;

#КонецОбласти

#КонецЕсли
