#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСеанса.СписокСсылокПравила = СформироватьСписокСсылокПравила();
	
	ОбновитьСоставТребованийПриЗаписиПравила();
	
	Если ЭтоНовый() Тогда
		УстановитьКодПравила();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьСоставТребованийПриЗаписиПравила()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТребованияРеализацияТребования.Ссылка КАК СсылкаНаТребование
	|ИЗ
	|	Справочник.Требования.РеализацияТребования КАК ТребованияРеализацияТребования
	|ГДЕ
	|	ТребованияРеализацияТребования.ПравилоПроверки = &ПравилоПроверки";
	
	Запрос.УстановитьПараметр("ПравилоПроверки", Ссылка);
	МассивТребований = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СсылкаНаТребование");
	Если МассивТребований.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныйМассивОшибок = Ссылка.ОбнаруживаемыеОшибки.ВыгрузитьКолонку("Ошибка");
	ТекущийМассивОшибок = ОбнаруживаемыеОшибки.ВыгрузитьКолонку("Ошибка");
	
	ОбновитьСоставТребованийККонфигурации(МассивТребований, ИсходныйМассивОшибок, ТекущийМассивОшибок);
	
КонецПроцедуры

Процедура УстановитьКодПравила()
	
	Префикс = НСтр("ru='Внеш_'");
	
	ПоследнийНомер = ПолучитьПоследнийНомерПравила(Префикс);
	НовыйНомер = ПоследнийНомер + 1;
	
	КоличествоНулей = Метаданные.Справочники.Правила.ДлинаКода - СтрДлина(Префикс);
	
	Код = Префикс + Формат(НовыйНомер, СтрШаблон("ЧЦ=%1; ЧВН=; ЧГ=0", КоличествоНулей));
	
КонецПроцедуры

Функция ПолучитьПоследнийНомерПравила(Префикс)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Правила.Код КАК Код
	|ИЗ
	|	Справочник.Правила КАК Правила
	|ГДЕ
	|	Правила.Код ПОДОБНО &Код
	|УПОРЯДОЧИТЬ ПО
	|	Код УБЫВ";
	
	Запрос.УстановитьПараметр("Код", Префикс + "%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПоследнийНомер = 0;
	
	Если Выборка.Следующий() Тогда
		ПоследнийНомер = СтрЗаменить(Выборка.Код, Префикс, "");
	КонецЕсли;
	
	Попытка
		ПоследнийНомер = Число(ПоследнийНомер);
	Исключение
		ПоследнийНомер = 0;
	КонецПопытки;
	
	Возврат ПоследнийНомер;
	
КонецФункции

#КонецОбласти

#КонецЕсли