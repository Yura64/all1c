#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСеанса.СписокСсылокТребования = СформироватьСписокСсылокТребования();
	
	ОбновитьСоставТребованийПриЗаписиТребования();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьСоставТребованийПриЗаписиТребования()
	
	МассивТребований = Новый Массив;
	МассивТребований.Добавить(Ссылка);
	
	ИсходныйМассивПравил = Ссылка.РеализацияТребования.ВыгрузитьКолонку("ПравилоПроверки");
	ТекущийМассивПравил = РеализацияТребования.ВыгрузитьКолонку("ПравилоПроверки");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПравилаОбнаруживаемыеОшибки.Ошибка КАК Ошибка
	|ИЗ
	|	Справочник.Правила.ОбнаруживаемыеОшибки КАК ПравилаОбнаруживаемыеОшибки
	|ГДЕ
	|	ПравилаОбнаруживаемыеОшибки.Ссылка В (&МассивПравил)
	|	И НЕ ПравилаОбнаруживаемыеОшибки.Ссылка.РучнаяПроверка
	|	И НЕ ПравилаОбнаруживаемыеОшибки.Ссылка.ПолуавтоматическаяПроверка
	|	И НЕ ПравилаОбнаруживаемыеОшибки.Ссылка.ПометкаУдаления
	|	И ПравилаОбнаруживаемыеОшибки.Ссылка.ИспользуетсяПриПроверке";
	
	Запрос.УстановитьПараметр("МассивПравил", ИсходныйМассивПравил);
	ИсходныйМассивОшибок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ошибка");
	
	Запрос.УстановитьПараметр("МассивПравил", ТекущийМассивПравил);
	ТекущийМассивОшибок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ошибка");
	
	ОбновитьСоставТребованийККонфигурации(МассивТребований, ИсходныйМассивОшибок, ТекущийМассивОшибок);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли