#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область Обработчики

// Выполняет проверку конфигурации в соответствии с переданным списком требований.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Результат = ИнтеграцияССППР.ИнициализироватьСтруктуруДляИнтеграцииВСППР(Конфигурация);
	Если НЕ Результат.Успешно Тогда
		Сообщить(Результат.ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	СтруктураПроверкиДляРегистрацииВСППР = Результат.СтруктураПроверкиДляРегистрацииВСППР;
	
	ТаблицаЖурнала = Проверка.ИнициализироватьТаблицуЖурналаПроверки();
	
	СтруктураПроверки = Новый Структура;
	СтруктураПроверки.Вставить("ПроверкаВерсии", ЭтотОбъект);
	СтруктураПроверки.Вставить("ТаблицаЖурнала",  ТаблицаЖурнала);
	
	Сценарий = Конфигурация.Сценарий;
	Если (Сценарий = 0) ИЛИ (Сценарий = 2) Тогда
		Проверка.ОбработкаПроведенияДокументаПроверки(СтруктураПроверки, СтруктураПроверкиДляРегистрацииВСППР);
	КонецЕсли;
	
	Если Сценарий > 0 Тогда
		Для Каждого СтрокаРасширения Из Конфигурация.Расширения Цикл
			Расширение = СтрокаРасширения.Расширение;
			Проверка.ОбработкаПроведенияДокументаПроверки(СтруктураПроверки, СтруктураПроверкиДляРегистрацииВСППР, Расширение);
		КонецЦикла;
	КонецЕсли;
	
	Проверка.СортироватьЖурналПроверки(ЭтотОбъект, СтруктураПроверки);
	
КонецПроцедуры

// Обработчик перед записью документа. Синхронизирует информацию конфигурации по версии.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ВерсияКонфигурации.Владелец <> Конфигурация Тогда
		Конфигурация = ВерсияКонфигурации.Владелец;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли